#*******************************************
# MacVersion = 1.02
# MacDescription = PrepareFieldNotes
# MacAuthor = roztocil
# MacFileName = BR_PrepareFieldNotes.gsk
# MacUrl = http://www.xlii.cz/downloads/gsak/macros/BR_PrepareFieldNotes.gsk
#---------------------------------------------------------------------------
# 1.01 2010-12-26:
#    - Doplnìno generování logù typu "Didn't Find It".
#    - Dopnìno generování správného "Found it" textu podle druhu keše.
# 1.02 2013-04-07:
#    - Oprava generování èasové známky u nepøesnì urèených èasù nálezu.
#*******************************************

DECLARE var=$NULL type=NUMERIC
$NODATE = [00000000]
$NL = CHR(13) + CHR(10)

GOSUB name=ReadForm

IF NOT($AppendToFile) AND FileExists($TargetFieldNotesFile)
  FileErase file=$TargetFieldNotesFile onerror=ABORT
  # zapíšeme BOM
  $result = AppendFile($TargetFieldNotesFile, UTF16("", "e"))
  GOSUB name=CheckFileOpResult
ENDIF

GOTO position=TOP
WHILE NOT($_EOL)
  $par1 = $d_foundByMeDate
  IF $par1 = $NODATE
    $par1 = $d_DnfDate
  ENDIF
  $par2 = $d_UserData
  $par3 = $UtcOffset
  GOSUB name=CalcTimeStamp

  BEGINCASE
    # další typy logù:
    #   Needs Archived
    #   Needs Maintenance
    #   Owner Maintenance
    CASE NOT($d_DNF) AND NOT($d_found)
      $logType = "Write note"
    CASE $d_DNF
      $logType = "Didn't find it"
    #-----------------------------------------
    # FOUND IT se liší podle typy keše:
    #   A = Project Ape         L = Locationless     V = Virtual
    #   B = Letterbox           M = Multi            W = Webcam
    #   C = Cache In Trash Out  O = Other            X = Maze Exhibit
    #   E = Event               R = Earth            Y = Waymark
    #   G = BenchMark           T = Traditional      Z = Mega event
    #   I = Wherigo             U = Unknown/Mystery
    #
    CASE AT($d_cacheType, "LIMRTU") > 0
      # Standardní keše.
      $logType = "Found it"
    CASE AT($d_cacheType, "CEZ") > 0
      # Eventy.
      $logType = "Attended"
    CASE $d_cacheType = "W"
      # Webcam keše.
      $logType = "Webcam photo taken"
    OTHERWISE
      # Ostatní snad jsou podobné standardním... :-\
      $logType = "Found it"
  ENDCASE

  # Pøipravíme základ textu - kvùli tomuhle (a datumu, taky) to hlavnì dìláme...  
  $logText = ""
  IF $d_userSort <> $NULL
    GOSUB name=LogTextSeparator
    $logText = "$logText#$d_userSort"
  ENDIF
  IF NOT(ISEMPTY($d_userData))
    GOSUB name=LogTextSeparator
    $logText = "$logText$d_userData - "
  ENDIF
  IF NOT(ISEMPTY($BlogUrl))
    GOSUB name=LogTextSeparator
    $logText = "$logText[url=$BlogUrl][size=1][i]more...[/i][/size][/url]"
  ENDIF

  $line = "$d_code,$timeStamp,$logType,"
  $line = $line + QUOTE("$logText")
  $line = "$line$NL"
  $line = UTF16($line, "e")
  # Odstraníme BOM
  $line = SUBSTR($line, 3, 0)

  $result = AppendFile($TargetFieldNotesFile, $line)
  GOSUB name=CheckFileOpResult
  GOTO position=NEXT
ENDWHILE

BEGINSUB name=CalcTimeStamp
  $xx_date = $par1
  $xx_hour = VAL(SUBSTR(REGEXREPLACE("[^.:0-9]", $par2, ""), 1, 2))
  $xx_min = VAL(SUBSTR(REGEXREPLACE("[^.:0-9]", $par2, ""), 4, 2))
  $xx_hour = VAL(REGEXSUB("\d+", $par2, 1, 0))
  $xx_min = VAL(REGEXSUB("\d+", $par2, 2, 0))
  $xx_off = VAL($par3)

  $xx_hour = $xx_hour + $xx_off
  WHILE $xx_hour < 0
    $xx_hour = $xx_hour + 24
    $xx_date = $xx_date - 1
  ENDWHILE
  WHILE $xx_hour > 24
    $xx_hour = $xxhour - 24
    $xx_date = $xx_date + 1
  ENDWHILE

  $timeStamp = DATETOSTRING($xx_date)
  $timeStamp = SUBSTR($timeStamp, 1, 4) + "-" + SUBSTR($timeStamp, 5, 2) + "-" + SUBSTR($timeStamp, 7, 2)
  $timeStamp = $timeStamp + "T" + STR($xx_hour, 2, 0) + ":" + STR($xx_min, 2, 0) + "Z"
  $timeStamp = REPLACE(" ", "0", $timeStamp)
ENDSUB

BEGINSUB name=LogTextSeparator
  IF ISEMPTY($logText) OR $logText = ""
    EXITSUB
  ENDIF
  IF ASC(RIGHT($logText, 1)) <= 32
    EXITSUB
  ENDIF
  $logText = "$logText "
ENDSUB

BEGINSUB name=CheckFileOpResult
  IF LEFT($result, 7) = "*Error*"
    PAUSE msg=$result
    CANCEL
  ENDIF
ENDSUB

BEGINSUB name=ReadForm
  MacSettings type=R filecheck=N vars=TargetFieldNotesFile,BlogUrl,UtcOffset,AppendToFile
  WHILE 1=1
    $result = Form($mainForm, "")
    BEGINCASE
      CASE $result = "OkButton"
        IF IsEmpty($UtcOffset) OR REGEX("[-+]\d+", $UtcOffset)
          MacSettings type=S vars=TargetFieldNotesFile,BlogUrl,UtcOffset,AppendToFile
          BREAK
        ENDIF
        MSGOK msg="UTC Offset must be empty or an integer with a sign."
      OTHERWISE
        RETURN
    ENDCASE
  ENDWHILE
ENDSUB

<Data> VarName=$mainForm
#********************************************************************
# Form generated by GSAK form designer on po 15-XI-2010 13:33:02
#********************************************************************

Name = Form1
  Type = Form
  Caption = Prepare Logging FieldNotes
  Height = 160
  Width = 492

Name = Label1
  Type = Label
  Height = 13
  Left = 8
  Top = 16
  Width = 109
  Caption = Target Field Notes File:

Name = TargetFieldNotesFile
  Type = File
  Height = 21
  Left = 128
  Top = 8
  Width = 345
  Taborder = 8

Name = Label2
  Type = Label
  Height = 13
  Left = 8
  Top = 40
  Width = 111
  Caption = UTC/GMT Time Offset:

Name = UtcOffset
  Type = Edit
  Height = 21
  Left = 128
  Top = 32
  Width = 33
  Taborder = 9

Name = AppendToFile
  Type = Checkbox
  Height = 17
  Left = 376
  Top = 32
  Width = 97
  Taborder = 10
  Caption = Append to File

Name = Label3
  Type = Label
  Height = 13
  Left = 8
  Top = 64
  Width = 109
  Caption = Blog URL:

Name = BlogUrl
  Type = Edit
  Height = 21
  Left = 128
  Top = 56
  Width = 345
  Taborder = 8

Name = OkButton
  Type = Button
  Enter = Yes
  Height = 25
  Left = 296
  Top = 88
  Width = 75
  Taborder = 11
  Caption = OK

Name = CancelButton
  Type = Button
  Escape = Yes
  Height = 25
  Left = 392
  Top = 88
  Width = 75
  Taborder = 12
  Caption = Cancel

<enddata>
