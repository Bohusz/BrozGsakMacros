#******************************************************************************************
# MacVersion = 1.01
# MacDescription = Download FieldNotes from Geocaching.com
# MacAuthor = Bohusz
# MacFileName = BR_DownloadPendingFieldNotes.gsk
# MacUrl =
#******************************************************************************************
# 2022-07-17 1.01 - Možnost proèištìní Field Notes na Geocaching.com.
#******************************************************************************************
#
$OUTPUT_FILENAME = "$_appData\fieldNotes\pending\pending_field-notes_#TSTAMP#.txt"
$DOWNLOAD_LOGDRAFTS_FIELDS  = "referenceCode,geocacheCode,loggedDate,geocacheLogType,note"
$TAKE_MAX = 50
$ARRAY_SIZE = 10000
#
GOSUB name=resetFieldNotesVariables
$v_skip = 0
$v_batchRecords = -1
$v_totalRecords = 0
ARRAY(1, 0) = "$ARRAY_SIZE"
ARRAY(1, -1) = ""
#
$p_datetime = DATETOSTRING($_today)+$_time
GOSUB name=toTStamp
$v_outputFileName = REPLACE("#TSTAMP#", $r_tstamp, $OUTPUT_FILENAME)
$p_filename = $v_outputFileName
GOSUB name=PrepareFile
#
## WHILE $v_skip = 0
WHILE $v_batchRecords < 0 OR $v_batchRecords = $TAKE_MAX
   $v_batchRecords = 0  
   $v_caption = "Stahuji field notes " + NUMTOSTR($v_skip + 1) + " až " + NUMTOSTR($v_skip + $TAKE_MAX) + " z Geocaching.com"
   $v_apiCall = "logdrafts?skip=$v_skip&take=50&fields=$DOWNLOAD_LOGDRAFTS_FIELDS"
   ## MSGOK msg=$v_apiCall
   $v_response = GCAPI2($v_apiCall, "GET", "", $v_caption)
   ## MSGOK msg=$v_response
   #
   $v_response = ""
   $v_status = SQLITE("sql", "SELECT key, data FROM gcApi2", "sqlget=4")
   WHILE NOT($_SqlEol4)
      $v_key  = SQLGET("key", 4)
      $v_data = SQLGET("data",4)
      $v_response = "$v_response$v_key: $v_data$_CRLF"
      SQLNEXT handle=4
      BEGINCASE
      CASE $v_key = "/referenceCode"
	 $v_refCode = $v_data
      CASE $v_key = "/geocacheCode"
	 $v_cacheCode = $v_data
      CASE $v_key = "/loggedDate"
	 $v_logTime = REGEXREPLACE("\.\d+$", $v_data, "")
      CASE $v_key = "/geocacheLogType/name"
	 $v_logType = $v_data
      CASE $v_key = "/note"
	 $v_logText = QUOTE(REPLACE($_quote, "'", $v_data))
         #
         # "/note" je poslední položka => zapíšeme do souboru-
         #
         $p_text = $v_logtext
         GOSUB name=normalizeLogText
         $p_text = "$v_cacheCode,$v_logTime,$v_logType,$r_text$_CRLF"
         ARRAY(1, $v_totalRecords+1) = "$v_refCode,$p_text"
         GOSUB name=WriteTextToFile
         #
         $v_batchRecords = $v_batchRecords + 1
         $v_totalRecords = $v_totalRecords + 1
         GOSUB name=resetFieldNotesVariables
      ENDCASE
   ENDWHILE
   SQLGETCLOSE handle=4
   $v_skip = $v_totalRecords
ENDWHILE

$p_question = "<p>$v_totalRecords naètených field notes uloženo do souboru</p><br/>"
$p_question = "$p_question<p><b>$v_outputFileName</b>.</pre><br/>"
$p_question = "$p_question<p>Chcete je naèíst do tabulky PublishLogs?</p>"
$p_question = "$p_question<p>Nebo chcete odstranit již zalogované položky?</p>"  
GOSUB name=readForm

BEGINSUB name=resetFieldNotesVariables
   $v_refCode = ""
   $v_cacheCode = ""
   $v_logTime = ""
   $v_logType = ""
   $v_logText = ""
ENDSUB

BEGINSUB name=toTStamp
   $xx_datetime = REGEXREPLACE("[^0-9Z]", UPPER($p_datetime), "")
   $r_tstamp = SUBSTR($xx_datetime,1,8)+"-"+SUBSTR($xx_datetime,9,0)
   ## CANCEL msg="$p_datetime$_CRLF$xx_datetime$_CRLF$r_tstamp"
ENDSUB
   
BEGINSUB name=normalizeLogText
   $xx_re  = "\[url=([^\]]+)\](.*)\[/url\]"
   $r_text = REGEXREPLACE($xx_re, $p_text, "[$2]($1)", "$")
   $r_text = REPLACE("[i]",      "*", $r_text)
   $r_text = REPLACE("[/i]",     "*", $r_text)
   $r_text = REPLACE("[size=1]", "",  $r_text)
   $r_text = REPLACE("[/size]",  "",  $r_text)
ENDSUB

BEGINSUB name=removeLogged
   $v_removed = 0
   $i = 1
   WHILE $i <= $ARRAY_SIZE AND ARRAY(1, $i) <> ""
      $v_line = ARRAY(1, $i)
      $v_refCode   = CSVGET($v_line, "1")
      $v_cacheCode = CSVGET("*", "2")
      $v_logDate   = SUBSTR(CSVGET("*", "3"), 1, 10)
      $v_logType   = REPLACE("'", "''", CSVGET("*", "4"))

      $x = SQLITE("sql", "SELECT * FROM logs WHERE lparent = '$v_cacheCode' AND ldate = '$v_logDate' AND lisowner AND UPPER(ltype) = UPPER('$v_logType')", "sqlget=1")
      IF NOT($_SqlEol1)
         $v_cache   = sqlget("lparent", 1)
         $v_date    = sqlget("ldate", 1)
         $v_logger  = sqlget("lby", 1)
         $v_logtype = sqlget("ltype", 1)
         $msg = "$v_cache: $v_logType $v_date $v_logger"
      ELSE
         $msg = "NOTFOUND"
      ENDIF
      SQLGETCLOSE handle=1

      IF $msg <> "NOTFOUND"
         SHOWSTATUS msg="$msg" height=500 width=350 mtype=MULTI display=ON title="Odstranìní již zalogovaných Field Notes"
         $x = GCAPI2("logdrafts/$v_refCode", "DELETE", "{}", "Removing Field Note $v_refCode")
         IF $_GcApiError
            MSGOK msg=$x
            BREAK
         ENDIF
         $v_removed = $v_removed + 1
      ENDIF
      $i = $i + 1
   ENDWHILE

   SHOWSTATUS msg="" height=500 width=350 mtype=MULTI display=ON title="Odstranìní již zalogovaných Field Notes"
   SHOWSTATUS msg="Odstranìno $v_removed FieldNotes." height=500 width=350 mtype=MULTI display=ON title="Odstranìní již zalogovaných Field Notes"
   DELAY ms=7500
ENDSUB

BEGINSUB name=readForm
   WHILE TRUE
      ## MSGOK msg=$v_response
      $form = EDITFORM($form, "message", "Caption", $p_question)
      $r_response = FORM($form,"")

      BEGINCASE
      CASE $r_response = "btnLoad"
         GcPublishFetch type=FILE file=$v_outputFileName
         BREAK
      CASE $r_response = "btnRemove"
         GOSUB name=removeLogged
         BREAK
      OTHERWISE
         BREAK
      ENDCASE
   ENDWHILE
ENDSUB

<Data> VarName=$form
Name = Form1
  Type = Form
  Delay = 120
  Height = 175
  Width = 675
  Caption = Field Notes naèteny

Name = message
  Type = Label
  Left = 8
  Top = 8
  Width = 332
  Height = 100
  Caption = xxx

Name = btnLoad
  Type = Button
  Height = 25
  Left = 8
  Top = 110
  Width = 150
  Caption = Naèíst do Publish Logs

Name = btnRemove
  Type = Button
  Height = 25
  Left = 166
  Top = 110
  Width = 150
  Caption = Odstranit již zalogované

Name = btnCancel
  Type = Button
  Height = 25
  Left = 585
  Top = 110
  Width = 75
  Escape = Yes
  Caption = Storno

<enddata>


##############################################################################
##
## PØEVZATO Z "BR_PrepareFieldNotes.gsk"
##
BEGINSUB name=PrepareFile
  IF FileExists($p_filename)
     FileErase file=$p_filename onerror=ABORT
  ENDIF
  # zapíšeme BOM
  $result = AppendFile($p_filename, UTF16("", "e"))
  GOSUB name=CheckFileOpResult
ENDSUB

BEGINSUB name=WriteTextToFile
  $xx_text = UTF16($p_text, "e")
  # Odstraníme BOM
  $xx_text = SUBSTR($xx_text, 3, 0)
  $result = AppendFile($p_filename, $xx_text)
  GOSUB name=CheckFileOpResult
ENDSUB

BEGINSUB name=CheckFileOpResult
  IF LEFT($result, 7) = "*Error*"
    PAUSE msg=$result
    CANCEL
  ENDIF
ENDSUB
