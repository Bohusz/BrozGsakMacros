#*******************************************
# MacVersion = 2.00
# MacDescription = Tabulka keší do blogger.com
# MacAuthor = Bob
# MacFileName = BR_HtmlMiniTable.gsk
# MacUrl = http://www.xlii.cz/downloads/gsak/macros/BR_HtmlMiniTable.gsk
#*******************************************

# inicializace
$NL = $_NewLine
GOSUB name=initStat
$html = "<table border='1' style='width: 100%; border-collapse: collapse;'>$NL"
$html = "$html<col width='20'/><col width='10'/><col width='40'/>$NL"
$myFoundItLog=0
$currentPosition = $d_Code

GOSUB name=VstupniFormular

IF $rbtLogDate
  $savedFilter = saveFilter()
  MFILTER where=code IN (SELECT lParent FROM logs WHERE date(lDate) BETWEEN date($datFrom) AND date($datUpto))
ENDIF

# zpracování
GOTO position=TOP
WHILE NOT($_EOL)
  IF $_FilterActive OR $d_UserFlag
    GOSUB name=getMyFoundItLog

    $html = "$html<tr>"
    IF $myFoundItLog <> 0
      $html = "$html<td align='right'><a href='http://www.geocaching.com/seek/log.aspx?LID=$myFoundItLog'>$d_UserData</a></td>"
    ELSE
      $html = "$html<td align='right'>$d_UserData</td>"
    ENDIF
    $html = "$html<td><img src='http://www.xlii.cz/img/gc/$d_CacheType.gif' alt='$d_CacheType' style='border: 0px; vertical-align: middle;'/></td>"
    IF isEmpty($d_url)
      $html = "$html<td>$d_Code</td>"
    ELSE
      $html = "$html<td><a href='$d_url'>$d_Code</a></td>"
    ENDIF
    $html = "$html<td><b>$d_Name</b> ($d_PlacedBy)</td>"
    $html = "$html</tr>$NL"
    GOSUB name=countStat
  ENDIF
  GOTO position=NEXT
ENDWHILE

# dokonèení
$html = "$html</table>$NL"
GOSUB name=htmlStat
$html = "$result$NL$NL<!-- more -->$NL$NL$html"

#úklid
IF $rbtLogDate
  IF restoreFilter($savedFilter) = 0
  ENDIF
END IF
IF NOT(seek($currentPosition))
ENDIF

# výsledek
CLIP DATA=$html

BEGINSUB name=getMyFoundItLog
  TABLE Active=Logs Scope=Parent

  $myFoundItLog=0
  WHILE NOT($_EOL)
    IF $d_lIsOwner AND $d_lType = "Found it"
      $myFoundItLog = $d_lLogid
    ENDIF
    GOTO position=NEXT
  ENDWHILE

  TABLE Active=Caches
ENDSUB

BEGINSUB name=initStat
  array(1, 0) = "256"
  array(1, -1) = "0"
  $date = [00000000]
ENDSUB

BEGINSUB name=countStat
  BEGINCASE
    CASE $d_found
      array(1, 1) = numToStr(val(array(1, 1)) + 1)
      array(1, asc($d_cacheType)) = numToStr(val(array(1, asc($d_cacheType))) + 1)
      IF $d_foundByMeDate > $date
        $date = $d_foundByMeDate
      ENDIF
    CASE $d_dnf
      array(1, 2) = numToStr(val(array(1, 2)) + 1)
      IF $d_dnfDate > $date
        $date = $d_dnfDate
      ENDIF
  ENDCASE
ENDSUB

BEGINSUB name=htmlStat
  IF array(1, 1) = "0" AND array(1, 2) = "0"
    EXITSUB
  ENDIF

  $result = "<table border='1' style='width: 100%; border-collapse: collapse;'>$NL"
  $result = "$result<col width='70'/><col width='70'/>$NL"
  $result = "$result<tr>$NL"
  $result = "$result<td style='color: white; background-color: #F87431;' align='center'><b>$date</b></td>$NL"
  $result = "$result<td style='background-color: white'>"

  IF array(1, 1) <> "0"
    $result = "$result<img src='http://www.xlii.cz/img/gc/f.png' alt='[FOUND]' style='border: 0px; vertical-align: middle;'/>"
    $result = "$result " + array(1, 1) + " "
  ENDIF
  IF array(1, 2) <> "0"
    $result = "$result<img src='http://www.xlii.cz/img/gc/dnf.gif' alt='[DNF]' style='border: 0px; vertical-align: middle;'/>"
    $result = " $result " + array(1, 2)
  ENDIF
  $result = "$result</td>$NL"
  $result = "$result<td style='background-color: white'>"

  $i = asc("A")
  WHILE $i <= asc("Z")
    IF array(1, $i) <> "0"
      $result = "$result"
      $result = "$result<img src='http://www.xlii.cz/img/gc/" + chr($i) + ".gif' alt='" + chr($i)+ "' style='border: 0px; vertical-align: middle;'/>"
      $result = "$result " + array(1, $i) + " "
    ENDIF
    $i = $i + 1
  ENDWHILE
  IF NOT(ISEMPTY($result))
    $result = "$result</td>$NL"
    $result = "$result</tr></table>"
  ENDIF
ENDSUB

BEGINSUB name=VstupniFormular
  $rbtAll = true
  $rbtLogDate = false
  $chbHeading = true
  $chbTable = true
  $chbLogs = true
  WHILE true
    #IF $rbtLogDate
    #  $VstupniFormular = editForm($VstupniFormular, "datFrom", "Enabled", "Yes")
    #  $VstupniFormular = editForm($VstupniFormular, "datUpto", "Enabled", "Yes")
    #ELSE
    #  $VstupniFormular = editForm($VstupniFormular, "datFrom", "Enabled", "No")
    #  $VstupniFormular = editForm($VstupniFormular, "datUpto", "Enabled", "No")
    #ENDIF

    $exit = form($VstupniFormular, "")
    BEGINCASE
    CASE $exit = "btnOk" OR $exit = "DelayExit"
      BREAK
    CASE $exit = "btnCancel" OR $exit = "SystemExit"
      RETURN
    ENDCASE
  ENDWHILE
ENDSUB

<Data> VarName=$VstupniFormular
#********************************************************************
# Form generated by GSAK form designer on so 19-III-2011 10:14:38
#********************************************************************

Name = Form1
  Type = Form
  Caption = Tabulky do blogu
  Height = 193
  Width = 358
  Delay = 7

Name = Groupbox1
  Type = Groupbox
  Caption = Co všechno generovat
  Height = 97
  Left = 192
  Top = 8
  Width = 137
  Taborder = 12

Name = rbtAll
  Type = Radiobutton
  Height = 17
  Left = 8
  Top = 8
  Width = 113
  Taborder = 8
  ExitOnChange = true
  Caption = Všechny ve výbìru

Name = rbtLogDate
  Type = Radiobutton
  Height = 17
  Left = 8
  Top = 32
  Width = 113
  Taborder = 9
  ExitOnChange = true
  Caption = Podle data logu

Name = datFrom
  Type = Date
  Height = 21
  Left = 48
  Top = 56
  Width = 121
  Taborder = 10

Name = datUpto
  Type = Date
  Height = 21
  Left = 48
  Top = 80
  Width = 121
  Taborder = 11

Name = Label1
  Type = Label
  Height = 13
  Left = 24
  Top = 56
  Width = 12
  Caption = od

Name = chbHeading
  Type = Checkbox
  Height = 17
  Left = 200
  Top = 32
  Width = 97
  Taborder = 13
  Caption = záhlaví (souhrn)

Name = chbTable
  Type = Checkbox
  Height = 17
  Left = 200
  Top = 56
  Width = 97
  Taborder = 14
  Caption = podrobnou tabulku

Name = chbLogs
  Type = Checkbox
  Height = 17
  Left = 200
  Top = 80
  Width = 97
  Taborder = 15
  Caption = logy

Name = Label2
  Type = Label
  Height = 13
  Left = 24
  Top = 80
  Width = 12
  Caption = do

Name = btnOk
  Type = Button
  Enter = Yes
  Height = 25
  Left = 168
  Top = 120
  Width = 75
  Taborder = 16
  Caption = Ok

Name = btnCancel
  Type = Button
  Escape = Yes
  Height = 25
  Left = 256
  Top = 120
  Width = 75
  Taborder = 17
  Caption = Storno

<enddata>
