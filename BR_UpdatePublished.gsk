#*******************************************
# MacVersion = 1.3
# MacDescription = BR_UpdatePublished
# MacAuthor = roztocil
# MacFileName = BR_UpdatePublished.gsk
# MacUrl =
#*******************************************
# 2018-11-04, 1.3 - pokud políèka (Published, ArchivedOn) dosud neexistují, jsou vytvoøena.
#                 - použití "konstant" s názvy políèek místo literálù rozptýlených po kódu
# 2015-12-31, 1.2 - podpora pro datum archivace
#                 - oprava: datum nálezu pøevádìno na interní formát
#                 - oprava: WHILE místo IF, který zbyl z doby, kdy šlo jen o datum publikace
###########################################################################
#
# KONSTANTY
#
# Custom Fields:
#
$PUBLISHED_DATE = "Published"
$ARCHIVED_DATE  = "ArchivedOn"

# Log types:
#
$ANNOUNCEMENT_LOG = "Announcement"
$ARCHIVE_LOG = "Archive"
$ATTENDED_LOG = "Attended"
$DNF_LOG = "Didn't find it"
$ENABLE_LOG = "Enable Listing"
$FOUND_LOG = "Found it"
$NEEDS_ARCHIVED_LOG = "Needs Archived"
$NEEDS_MAINTENANCE_LOG = "Needs Maintenance"
$OWNER_MAINTENANCE_LOG = "Owner Maintenance"
$REVIEWER_LOG = "Post Reviewer Note"
$PUBLISH_LOG = "Publish Listing"
$DISABLE_LOG = "Temporarily Disable Listing"
$UNARCHIVE_LOG = "Unarchive"
$UPDATE_LOG = "Update Coordinates"
$PHOTO_TAKEN_LOG = "Webcam Photo Taken"
$WILL_ATTEND_LOG = "Will Attend"
$WRITE_NOTE_LOG = "Write note"

# Inicializace.
#
$p_customField = $PUBLISHED_DATE
GOSUB name=prepareCustomField

$p_customField = $ARCHIVED_DATE
GOSUB name=prepareCustomField

# Processing.
#
TRANSACTION action=BEGIN
SPEEDMODE status=on

$i = 0
GOTO position=TOP
WHILE NOT($_EOL)
  $i = $i + 1
  SHOWSTATUS msg="$i/$_Count $d_code - $d_name"
  $v_published = CustomGet($PUBLISHED_DATE)
  $v_archived  = CustomGet($ARCHIVED_DATE)


  #IF ($v_published = "")
    $r = SQLITE("sql", "SELECT lDate, lType FROM logs WHERE (lType IN ('$PUBLISH_LOG', '$ARCHIVE_LOG') OR lType = '$FOUND_LOG' AND lIsOwner) AND lParent = '$d_Code'", "sqlget=0")
    WHILE NOT($_SQLEOL)
      $v_logType = SQLGET("ltype")
      $v_logDate = SQLGET("lDate")
      $r = ""
      #MSGOK msg="[$d_code] $v_logType: $v_logDate"

      BEGINCASE

      CASE $v_logType = $PUBLISH_LOG
          $r = CustomPut($PUBLISHED_DATE, $v_logDate)

      CASE $v_logType = $ARCHIVE_LOG
          $r = CustomPut($ARCHIVED_DATE, $v_logDate)

      CASE $v_logType = $FOUND_LOG
          IF ($d_FoundByMeDate = [00000000])
            $d_FoundByMeDate = SQLTODATE($v_logDate)
          ENDIF    
      ENDCASE

      IF LEFT($r, 7) = "*Error*"
        SPEEDMODE status=off
        TRANSACTION action=END
        CANCEL msg=$r
      ENDIF

      SQLNEXT
    ENDWHILE
    SQLGETCLOSE
  #ENDIF

  GOTO position=NEXT
ENDWHILE
 
SPEEDMODE status=off
TRANSACTION action=END
GOTO position=TOP

BEGINSUB name=prepareCustomField
  # $p_customField - jméno políèka
  #
  IF CustomConfig("Get", $p_customField) = ""
    $x = CustomConfig("Add", "$p_customField,date,Global")
  ENDIF
ENDSUB