#*******************************************
# MacVersion = 1.5
# MacDescription = BR_UpdatePublished
# MacAuthor = roztocil
# MacFileName = BR_UpdatePublished.gsk
# MacUrl =
#*******************************************
# 2022-07-17, 1.5 - oprava: aby mohlo b˝t zahrnuto, musÌ b˝t "Attended" a "Webcam Photo Taken"
#                           takÈ vyhled·v·no, ûe
# 2022-07-12, 1.4 - zjiöùuje takÈ, zda jiû m·m zalogov·no, ukl·d· se do "FoundItLogged"
#                 - oprava: do "nalezeno" zahrnuto i "Attended" a "Webcam Photo Taken"
# 2018-11-04, 1.3 - pokud polÌËka (Published, ArchivedOn) dosud neexistujÌ, jsou vytvo¯ena.
#                 - pouûitÌ "konstant" s n·zvy polÌËek mÌsto liter·l˘ rozpt˝len˝ch po kÛdu
# 2015-12-31, 1.2 - podpora pro datum archivace
#                 - oprava: datum n·lezu p¯ev·dÏno na internÌ form·t
#                 - oprava: WHILE mÌsto IF, kter˝ zbyl z doby, kdy ölo jen o datum publikace
###########################################################################
#
# KONSTANTY
#
# Custom Fields:
#
$PUBLISHED_DATE = "Published"
$ARCHIVED_DATE  = "ArchivedOn"
$FOUNDIT_LOGGED = "FoundItLogged"

# Log types:
#
$ANNOUNCEMENT_LOG = "Announcement"
$ARCHIVE_LOG = "Archive"
$ATTENDED_LOG = "Attended"
$DNF_LOG = "Didn't find it"
$ENABLE_LOG = "Enable Listing"
$FOUND_LOG = "Found it"
$NEEDS_ARCHIVED_LOG = "Needs Archived"
$NEEDS_MAINTENANCE_LOG = "Needs Maintenance"
$OWNER_MAINTENANCE_LOG = "Owner Maintenance"
$REVIEWER_LOG = "Post Reviewer Note"
$PUBLISH_LOG = "Publish Listing"
$DISABLE_LOG = "Temporarily Disable Listing"
$UNARCHIVE_LOG = "Unarchive"
$UPDATE_LOG = "Update Coordinates"
$PHOTO_TAKEN_LOG = "Webcam Photo Taken"
$WILL_ATTEND_LOG = "Will Attend"
$WRITE_NOTE_LOG = "Write note"

# Inicializace.
#
$p_customFieldName = $PUBLISHED_DATE
$p_customFieldType = "date"
GOSUB name=prepareCustomField

$p_customFieldName = $ARCHIVED_DATE
$p_customFieldType = "date"
GOSUB name=prepareCustomField

$p_customFieldName = $FOUNDIT_LOGGED
$p_customFieldType = "Boolean"
GOSUB name=prepareCustomField

# Processing.
#
TRANSACTION action=BEGIN
SPEEDMODE status=on

$i = 0
GOTO position=TOP
WHILE NOT($_EOL)
  $i = $i + 1
  SHOWSTATUS msg="$i/$_Count $d_code - $d_name"
  $v_published = CustomGet($PUBLISHED_DATE)
  $v_archived  = CustomGet($ARCHIVED_DATE)
  $v_fiLogged  = 0

  #IF ($v_published = "")
    $r = SQLITE("sql", "SELECT lDate, lType FROM logs WHERE (lType IN ('$PUBLISH_LOG', '$ARCHIVE_LOG') OR lType IN ('$FOUND_LOG', '$ATTENDED_LOG', '$PHOTO_TAKEN_LOG') AND lIsOwner) AND lParent = '$d_Code'", "sqlget=0")
    WHILE NOT($_SQLEOL)
      $v_logType = SQLGET("ltype")
      $v_logDate = SQLGET("lDate")
      $r = ""
      #MSGOK msg="[$d_code] $v_logType: $v_logDate"

      BEGINCASE

      CASE $v_logType = $PUBLISH_LOG
          $r = CustomPut($PUBLISHED_DATE, $v_logDate)
          GOSUB name=checkCustomOperation

      CASE $v_logType = $ARCHIVE_LOG
          $r = CustomPut($ARCHIVED_DATE, $v_logDate)
          GOSUB name=checkCustomOperation

      CASE $v_logType = $FOUND_LOG OR $v_logType = $ATTENDED_LOG OR $v_logType = $PHOTO_TAKEN_LOG
          IF ($d_FoundByMeDate = [00000000])
            $d_FoundByMeDate = SQLTODATE($v_logDate)
          ENDIF    
          $v_fiLogged = 1
      ENDCASE

      IF LEFT($r, 7) = "*Error*"
        SPEEDMODE status=off
        TRANSACTION action=END
        CANCEL msg=$r
      ENDIF

      SQLNEXT
    ENDWHILE
    SQLGETCLOSE
  #ENDIF
  $r = CustomPut($FOUNDIT_LOGGED, "$v_fiLogged")
  GOSUB name=checkCustomOperation

  GOTO position=NEXT
ENDWHILE
 
SPEEDMODE status=off
TRANSACTION action=END
GOTO position=TOP

BEGINSUB name=prepareCustomField
  # $p_customFieldName - jmÈno polÌËka
  # $p_customFieldType - jmÈno polÌËka
  #
  IF CustomConfig("Get", $p_customFieldName) = ""
    $r = CustomConfig("Add", "$p_customFieldName,$p_customFieldType,Global")
    GOSUB name=checkCustomOperation
  ENDIF
ENDSUB

BEGINSUB name=checkCustomOperation
  # $r - v˝sledek
  #
  IF $r <> ""
    PAUSE msg=$r
  ENDIF
ENDSUB
