#*******************************************
# MacVersion = 2.13
# MacDescription = Tabulka keší + my logs do blogger.com
# MacAuthor = Bohusz
# MacFileName = BR_HtmlLogBase2.gsk
# MacUrl = http://www.xlii.cz/downloads/gsak/macros/BR_HtmlLogBase.gsk
#*******************************************
# 1.10 (21.12.2010):
#    - Pozadí logù se støídá svìtlejší/základní/svìtlejší/...
# 1.11 (27.12.2010):
#    - Problém s apostrofem v typu DNF logu - nenašla se ikona
# 2.00 (19.3.2011):
#    - Možnost vybrat keše podle data logu (doposud musely být
#      vybrány pøedem).
#    - Vstupní formuláø umožòující vybrat režim (podle data/výbìr),
#      rozsah data a rozsah výsledku (hlavièka, tabulka, logy).
# 2.01 (4.7.2011)
#    - Možnost vybrat keše podle odkazu na blog v logu.
# 2.02 (27.11.2011)
#    - Výbìr blogu ze seznamu.
# 2.03 (19.5.2013)
#    - Za oddìlovaè záhlaví doplnìn prázdný øádek.
# 2.04 (29.10.2013)
#    - Podpora pro týmový odlov - èas ve tvaru HH:MM/HH:MM, kde první
#      je èas spojení týmu a druhé èas odlovu jinou èástí týmu
# 2.05 (6.1.2014)
#    - podpora odrážek
#    - oprava - zapoètení Attended do nálezù
# 2.06 (1.11.2015)
#    - Odstraòování GCVote poznámek.
# 2.07 (9.1.2016)
#    - Vyhledávání názvù blogù pomocí SQLGET (rychlejší).
#    - Podpora pro blogy shodných názvù.
#    - Pokud log neobsahuje èas (ne)nálezu, je doplnìn z GSAK.
#    - Oprava: Doplnìní podpory pro chybìjící barvy.
#    - Oprava: URL blogu nyní rozpoznáno i v TLD .cz, nejen .com (je to ekvivalentní).
# 2.08 (20.1.2016)
#    - Zahrnutí obrázkù pøiložených k logu do generovaného HTML (vèetnì podpory prettyPhoto).
#    - Zobecnìní detekce odkazu na GCVOTE (za úèelem jeho odstranìní).
# 2.09 (3.4.2016)
#    - Podpora pro Markdown (nový formát logù).
# 2.10 (9.10.2017)
#    - Oprava rozmìrù tabulky, zdá se, že se to zaèalo chápat jinak.
# 2.11 (28.12.2017)
#    - Drobné zmìny ve formátování Markdown, hlavnì citace a podpora podtržítek vedle hvìzdièek.
# 2.12 (16.12.2018)
#    - Naètení všech typù logù pro vybrané keše.
#      (Ještì to není dokonalé, pøi výbìru podle data se najdou všechny logy pro keše logované v tomto rozsahu.)
#    - Oznaèení jiných typù logù v mini tabulce kurzívou.
# 2.13 (23.05.2016)
#    - Oprava: nyní rozpozná i odkaz na blog s markupem uvnitø hranatých závorek.
#    - Oprava: GS zmìnil URL obrázkù v logu z https://img.geocaching.com/cache/log/OBRÁZEK
#      na https://img.geocaching.com/OBRÁZEK, takže pøestalo fungovat generování URL na thumbnails
#      (https://img.geocaching.com/cache/log/thumb/OBRÁZEK).
#*******************************************

# inicializace
VSUB status=on

# Array and List  identifiers
$STAT = 1
$ITEMS = 2
$WORK = 3
$IMAGES = 4
$BLOGS = "BLOGS"
#
$LOGREC_RE        = "^([-.0-9]+) +(~?\d?\d:\d\d(/\d?\d:\d\d)?)?,([^,]+),([^,]+),([^,]*)$"
$LOGREC_DATE_GRP  = 1
$LOGREC_TIME_GRP  = 2
$LOGREC_TYPE_GRP  = 4
$LOGREC_CACHE_GRP = 5
$LOGREC_LOGID_GRP = 6
#
$BLOGITEM_RE        = "^([-.0-9]+),([-.0-9]+),([^,]+)(,(\d+),(\d+))?$"
$BLOGITEM_DATE1_GRP = 1
$BLOGITEM_DATE2_GRP = 2
$BLOGITEM_NAME_GRP  = 3
$BLOGITEM_YEAR_GRP  = 5
$BLOGITEM_MONTH_GRP = 6
#
$URL_RE        = "\[url=http://kacer-bob.blogspot.(com|cz)/((\d+)/(\d+)|p)/([^.]+).html\]"
$URL_YEAR_GRP  = 3
$URL_MONTH_GRP = 4
$URL_NAME_GRP  = 5
#
$NL  = $_NewLine
$SEP = CHR(0)
$LightBackground = "#d8e8c8"
$OddLogRecord = TRUE
$VERBOSE = FALSE
# GOSUB name=initStat

GOSUB name=VstupniFormular

IF $rbtAll
  GOSUB name=getLimittingDates
ENDIF
IF $rbtLogDate
  $minDate = $datFrom
  $maxDate = $datUpto
ENDIF
IF $rbtLogUrl
  $p_url = $txtLogUrl
  GOSUB name=initSearchByUrl
ELSE
  $blogName  = ""
  $blogYear  = ""
  $blogMonth = ""
ENDIF

$savedFilter = saveFilter()
$currentPosition = $d_Code
IF NOT($rbtAll)
   MFILTER where=code IN (SELECT lParent FROM logs WHERE date(lDate) BETWEEN date($minDate) AND date($maxDate) AND lIsOwner) OR foundByMeDate BETWEEN date($mindate) AND date($maxDate) OR dnfDate BETWEEN date($minDate) AND date($maxDate)
ENDIF

GOSUB name=lookupData

$html = ""

IF $chbHeading
  GOSUB name=processStats
  $p_sectionName    = "STATISTICS"
  $p_sectionContent = $r_stats
  GOSUB name=appendSection
ENDIF

IF $chbHeading AND $chbTable
  $html = "$html$NL<!-- more -->$NL<br/>$NL"
ENDIF

IF $chbTable
  GOSUB name=processTable
  $p_sectionName    = "LOG OVERVIEW"
  $p_sectionContent = $r_table
  GOSUB name=appendSection
ENDIF

IF $chbLogs
  GOSUB name=processLogs
  $p_sectionName    = "LOGS"
  $p_sectionContent = $r_logs
  GOSUB name=appendSection
ENDIF

# úklid
$x = restoreFilter($savedFilter, true) = 0
$x = seek($currentPosition)

# výsledek
CLIP DATA=$html

#DELAY ms=20000

##################################################################################################################
##                                                                                   P Ø Í P R A V A   D A T    ##
##################################################################################################################

BEGINSUB name=lookupData
  array($ITEMS, 0) = "1024"
  array($ITEMS, -1) = "0"
  $g_maxItems = 0

  GOTO position=TOP
  WHILE NOT($_EOL)
    $v_processed = FALSE
    TABLE active=LOGS scope=PARENT
    WHILE NOT($_EOL)
      IF $d_lIsOwner
        $p_text = $d_lText
        GOSUB name=getBlogUrl
        $v_blogName  = $r_url
        $v_blogYear  = $r_year
        $v_blogMonth = $r_month

        IF $blogName <> ""
          IF $blogYear <> "" AND $blogYear <> ""
            $v_processed = $v_blogName <> $blogName OR $v_blogYear <> $blogYear OR $v_blogMonth <> $blogMonth
          ELSE
            $v_processed = $v_blogName <> $blogName
          ENDIF
        ENDIF

#        IF NOT($v_processed)
          $v_time = regexsub("(\d{1,2}:\d{2})", $d_lText, 1, 1)
          IF $v_time = ""
            # Nìkdy je keš v GSAK oznaèena jako DNF, ale není k ní buï žádný log nebo je jiného typu (typicky 'Needs maintenance') a bez èasu 'nenálezu'.
            $v_time = $d_UserData
          ENDIF
          IF len($v_time) = 4
            $v_time = "0$v_time"
          ENDIF
          $p_type = $d_lType
          GOSUB name=normalizeLogType
          $v_data = dateToString($d_lDate) + " $v_time,$r_type,$d_lParent,$d_lLogid"
          $g_maxItems = $g_maxItems + 1
          $p_msg = "[FETCH] #$g_maxItems: $d_code, LOGGED | $v_data"
          GOSUB name=showStatus
          array($ITEMS, $g_maxItems) = $v_data
          $v_processed = TRUE
#       ENDIF
      ENDIF
      GOTO position=NEXT
    ENDWHILE
    TABLE active=CACHES

    IF NOT($v_processed)
      $v_type = ""
      IF $d_found
        $v_type = "FOUND"
        $v_date = $d_FoundByMeDate
      ENDIF
      IF $d_DNF
        $v_type = "DNF"
        $v_date = $d_DNFDate
      ENDIF
      IF $v_type <> ""
        $v_time = $d_userData
        IF len($v_time) = 4
          $v_time = "0$v_time"
        ENDIF
        $v_data = dateToString($v_date) + " $v_time,$v_type,$d_code,"
        $g_maxItems = $g_maxItems + 1
        $p_msg = "[FETCH] #$g_maxItems: $d_code, NO LOG"
        GOSUB name=showStatus
        array($ITEMS, $g_maxItems) = $v_data
      ENDIF
    ENDIF

    GOTO position=NEXT
  ENDWHILE

  # seøadíme data: Ascending, String, Ignore case
  $p_array = $ITEMS
  $p_count = $g_maxItems
  GOSUB name=shrinkArray
  array($ITEMS, -2) = "ASI"

  GOSUB name=showStatus_emptyLine
ENDSUB

##################################################################################################################
##                                                                                       S T A T I S T I K Y    ##
##################################################################################################################

BEGINSUB name=processStats
  GOSUB name=calculateStats
  GOSUB name=htmlStats
  $r_stats = $result
ENDSUB

BEGINSUB name=calculateStats
  TABLE active=CACHES

  array($STAT, 0) = "256"
  array($STAT, -1) = "0"
  $g_date0 = [99991231]
  $g_date9 = [00000000]

  $v_index = 1
  WHILE $v_index <= $g_maxItems
    $v_item = array($ITEMS, $v_index)
    $p_msg = "[STATS] #$v_index: $v_item"
    GOSUB name=showStatus
    $v_date  = stringToDate(regexsub($LOGREC_RE, $v_item, 1, $LOGREC_DATE_GRP))
    $v_lgTyp = regexsub($LOGREC_RE, $v_item, 1, $LOGREC_TYPE_GRP)
    $v_cache = regexsub($LOGREC_RE, $v_item, 1, $LOGREC_CACHE_GRP)
    $p_msg = "[STATS] #$v_index: $v_cache, $v_lgTyp"
    GOSUB name=showStatus

    IF NOT(seek($v_cache))
      $v_msg = "Uh, oh! Keš $v_cache na pozici $v_index seznamu nebyla nalezena v databázi. To pøece není možné!"
      msgOk msg=$v_msg caption="Makro bude ukonèeno"
      RETURN
    ENDIF
    BEGINCASE
      CASE $v_lgTyp = "FOUND" OR $v_lgTyp = "ATTENDED" OR $v_lgTyp = "PHOTO TAKEN"
        array($STAT, 1) = numToStr(val(array($STAT, 1)) + 1)
        array($STAT, asc($d_cacheType)) = numToStr(val(array($STAT, asc($d_cacheType))) + 1)
      CASE $v_lgTyp = "DNF"
        array($STAT, 2) = numToStr(val(array($STAT, 2)) + 1)
      OTHERWISE
        $v_date = [00000000]
    ENDCASE
    IF $v_date <> [00000000]
      IF $v_date < $g_date0
        $g_date0 = $v_date
      ENDIF
      IF $v_date > $g_date9
        $g_date9 = $v_date
      ENDIF
    ENDIF

    $v_index = $v_index + 1
  ENDWHILE

  GOSUB name=showStatus_emptyLine
ENDSUB

BEGINSUB name=htmlStats
  IF array($STAT, 1) = "0" AND array($STAT, 2) = "0"
    $result = ""
    EXITSUB
  ENDIF

  $result = "<table border='1' style='width: 100%; border-collapse: collapse;'>$NL"
  IF $minDate <> $maxDate
    $result = "$result<col width='120'/><col width='70'/>$NL"
  ELSE
    $result = "$result<col width='100'/><col width='70'/>$NL"
  ENDIF
  $result = "$result<tr>$NL"
  $result = "$result<td style='color: white; background-color: #F87431;' align='center'><b>"
  IF $g_date0 <> $g_date9
    IF substr(dateToString($g_date0), 1, 4) <> substr(dateToString($g_date9), 1, 4)
      $result = "$result$g_date0"
    ELSE
      $result = $result + NumToStr(Val(substr(dateToString($g_date0), 7, 2))) + "."
      IF substr(dateToString($g_date0), 5, 2) <> substr(dateToString($g_date9), 5, 2)
        $result = $result + NumToStr(Val(substr(dateToString($g_date0), 5, 2))) + "."
      ENDIF
    ENDIF
    $result = "$result - "
  ENDIF
  $result = "$result$g_date9</b></td>$NL"
  $result = "$result<td style='background-color: white'>"

  IF array($STAT, 1) <> "0"
    $result = "$result<img src='http://www.xlii.cz/img/gc/f.png' alt='[FOUND]' style='border: 0px; vertical-align: middle;'/>"
    $result = "$result " + array($STAT, 1) + " "
  ENDIF
  IF array($STAT, 2) <> "0"
    $result = "$result<img src='http://www.xlii.cz/img/gc/dnf.gif' alt='[DNF]' style='border: 0px; vertical-align: middle;'/>"
    $result = " $result " + array($STAT, 2)
  ENDIF
  $result = "$result</td>$NL"
  $result = "$result<td style='background-color: white'>"

  $i = asc("A")
  WHILE $i <= asc("Z")
    IF array($STAT, $i) <> "0"
      $result = "$result"
      $result = "$result<img src='http://www.xlii.cz/img/gc/" + chr($i) + ".gif' alt='" + chr($i)+ "' style='border: 0px; vertical-align: middle;'/>"
      $result = "$result " + array($STAT, $i) + " "
    ENDIF
    $i = $i + 1
  ENDWHILE
  IF NOT(ISEMPTY($result))
    $result = "$result</td>$NL"
    $result = "$result</tr></table>"
  ENDIF
ENDSUB

##################################################################################################################
##                                                                                             T A B U L K A    ##
##################################################################################################################

BEGINSUB name=processTable
  TABLE active=CACHES

  $r_table = "<table border='1' style='width: 100%; border-collapse: collapse;'>$NL"
  IF $minDate <> $maxDate
    $r_table = "$r_table<col width='80px'/>"
  ENDIF
  $r_table = "$r_table<col width='40px'/><col width='20px'/><col width='80px'/>$NL"

  $v_prevDate = [00000000]
  $v_index = 1
  WHILE $v_index <= $g_maxItems
    $v_item  = array($ITEMS, $v_index)
    $v_date  = stringToDate(regexsub($LOGREC_RE, $v_item, 1, $LOGREC_DATE_GRP))
    $v_time  = regexsub($LOGREC_RE, $v_item, 1, $LOGREC_TIME_GRP)
    $v_lgTyp = regexsub($LOGREC_RE, $v_item, 1, $LOGREC_TYPE_GRP)
    $v_cache = regexsub($LOGREC_RE, $v_item, 1, $LOGREC_CACHE_GRP)
    $v_logid = regexsub($LOGREC_RE, $v_item, 1, $LOGREC_LOGID_GRP)
    $p_msg = "[TABLE] #$v_index: $v_cache, $v_date $v_time, $v_lgTyp, $v_logid"
    GOSUB name=showStatus

    $p_date = $v_date
    $p_time = $v_time
    $p_lgTyp = $v_lgTyp
    $p_cache = $v_cache
    $p_logid = $v_logid
    $p_prevDate = $v_prevDate
    GOSUB name=processTableItem
    
    $v_prevDate = $p_date
    $r_table = "$r_table$NL$r_item"

    $v_index = $v_index + 1
  ENDWHILE

  $r_table = "$r_table</table>$NL"

  GOSUB name=showStatus_emptyLine
ENDSUB

BEGINSUB name=processTableItem
  IF NOT(seek($p_cache))
    $v_msg = "Uh, oh! Keš $v_cache na pozici $v_index seznamu nebyla nalezena v databázi. To pøece není možné!"
    msgOk msg=$v_msg caption="Makro bude ukonèeno"
    RETURN
  ENDIF
  GOSUB name=htmlTableItem
ENDSUB

BEGINSUB name=htmlTableItem
  $r_item = "<tr>"

  # datum
  IF $minDate <> $maxDate
    GOSUB name=itemStyle_BEGIN
    IF $p_date <> $p_prevDate
      $r_item = "$r_item<td align='right'>$p_date</td>"
    ELSE
      $r_item = "$r_item<td align='center'>&quot;</td>"
    ENDIF
    GOSUB name=itemStyle_END
  ENDIF

  # èas + odkaz na log
  $r_item = "$r_item<td align='right'>"
  GOSUB name=itemStyle_BEGIN
  IF NOT(isempty($p_logid))
    $r_item = "$r_item<a href='http://www.geocaching.com/seek/log.aspx?LID=$p_logid'>$p_time</a>"
  ELSE
    $r_item = "$r_item$p_time"
  ENDIF
  GOSUB name=itemStyle_END
  $r_item = "$r_item</td>"

  # ikony keše
  $r_item = "$r_item<td>"
  GOSUB name=itemStyle_BEGIN
  $r_item = "$r_item<img src='http://www.xlii.cz/img/gc/$d_CacheType.gif' alt='$d_CacheType' style='border: 0px; vertical-align: middle;'/>"
  GOSUB name=itemStyle_END
  $r_item = "$r_item</td>"

  # GC kód + odkaz na listing
  $r_item = "$r_item<td>"
  GOSUB name=itemStyle_BEGIN
  IF isEmpty($d_url)
    $r_item = "$r_item$d_Code"
  ELSE
    $r_item = "$r_item<a href='$d_url'>$d_Code</a>"
  ENDIF
  GOSUB name=itemStyle_END
  $r_item = "$r_item</td>"

  # název keše (autor)
  $r_item = "$r_item<td>"
  GOSUB name=itemStyle_BEGIN
  GOSUB name=boldIfFoundOrNotFound_BEGIN
  $r_item = "$r_item$d_Name"
  GOSUB name=boldIfFoundOrNotFound_END
  $r_item = "$r_item ($d_PlacedBy)"
  GOSUB name=itemStyle_END
  $r_item = "$r_item</td>"

  $r_item = "$r_item</tr>$NL"
ENDSUB

BEGINSUB name=itemStyle_BEGIN
  BEGINCASE
  CASE $p_lgTyp = "DNF"
    $r_item = "$r_item<i><font color='#808080'>"
  CASE $p_lgTyp = "ATTENDED" OR $p_lgTyp = "FOUND" OR $p_lgTyp = "PHOTO_TAKEN"
  OTHERWISE
    $r_item = "$r_item<i>"
  ENDCASE
ENDSUB

BEGINSUB name=itemStyle_END
  BEGINCASE
  CASE $p_lgTyp = "DNF"
    $r_item = "$r_item</font></i>"
  CASE $p_lgTyp = "ATTENDED" OR $p_lgTyp = "FOUND" OR $p_lgTyp = "PHOTO_TAKEN"
  OTHERWISE
    $r_item = "$r_item</i>"
  ENDCASE
ENDSUB

BEGINSUB name=boldIfFoundOrNotFound_BEGIN
  IF $p_lgTyp = "DNF" OR $p_lgTyp = "ATTENDED" OR $p_lgTyp = "FOUND" OR $p_lgTyp = "PHOTO_TAKEN"
    $r_item = "$r_item<b>"
  ENDIF
ENDSUB

BEGINSUB name=boldIfFoundOrNotFound_END
  IF $p_lgTyp = "DNF" OR $p_lgTyp = "ATTENDED" OR $p_lgTyp = "FOUND" OR $p_lgTyp = "PHOTO_TAKEN"
    $r_item = "$r_item</b>"
  ENDIF
ENDSUB

##################################################################################################################
##                                                                                                   L O G Y    ##
##################################################################################################################

BEGINSUB name=processLogs
  $r_logs = ""

  $v_index = 1

  WHILE $v_index <= $g_maxItems
    $v_item  = array($ITEMS, $v_index)
    $v_date  = stringToDate(regexsub($LOGREC_RE, $v_item, 1, $LOGREC_DATE_GRP))
    $v_time  = regexsub($LOGREC_RE, $v_item, 1, $LOGREC_TIME_GRP)
    $v_lgTyp = regexsub($LOGREC_RE, $v_item, 1, $LOGREC_TYPE_GRP)
    $v_cache = regexsub($LOGREC_RE, $v_item, 1, $LOGREC_CACHE_GRP)
    $v_logid = regexsub($LOGREC_RE, $v_item, 1, $LOGREC_LOGID_GRP)
    $p_msg = "[LOGS] #$v_index: $v_cache, $v_date $v_time, $v_lgTyp, $v_logid"
    GOSUB name=showStatus

    # log nemusí vždy existovat - možná ještì nebyl naèten, pak generujeme jen z atributù záznamu keše v databázi
    IF NOT(isempty($v_logid))
      TABLE active=CACHES
      IF NOT(seek($v_cache))
        $v_msg = "Uh, oh! Keš $v_cache na pozici $v_index seznamu nebyla nalezena v databázi. To pøece není možné!"
        msgOk msg=$v_msg caption="Makro bude ukonèeno"
        RETURN
      ENDIF

      TABLE active=LOGS scope=PARENT
      IF NOT(seek($v_logId))
        $v_msg = "Uh, oh! Log $v_logId pro keš $v_cache na pozici $v_index seznamu nebyl nalezen v databázi. To pøece není možné!"
        msgOk msg=$v_msg caption="Makro bude ukonèeno"
        RETURN
      ENDIF

      $p_date = $v_date
      $p_time = $v_time
      $p_type = $v_lgTyp
      GOSUB name=htmlLog

      $r_logs = "$r_logs$r_logHtml$NL"
    ENDIF

    $v_index = $v_index + 1
  ENDWHILE

  TABLE active=CACHES

  GOSUB name=showStatus_emptyLine
ENDSUB

# htmlLog: Pøevede aktuálnì vybraný log do HTML.
#
# Pøi zpracování dáváme pøednost údajùm pøedaným jako parametry, protože mohly být oproti skuteènosti z nìjakého dùvodu zmìnìny.
#
# @param $p_date  Den, ke kterému mìl být log vytvoøen.
# @param $p_time  Èas, ke kterému mìl být log vytvoøen.
# @param $p_type  Typ logu.
#
# @global $OddLogRecord  Je log sudý nebo lichý? Má vliv na HTML vizualizaci.
#
# @return $r_logHtml
#
BEGINSUB name=htmlLog
  $r_logHtml = ""
  IF $OddLogRecord
    $r_logHtml = "$r_logHtml<div style='background: $LightBackground; outline: 5pt solid $LightBackground;'>"
  ELSE
    $r_logHtml = "$r_logHtml<div>"
  ENDIF
  $OddLogRecord = NOT($OddLogRecord)
  $p_iconType = $p_type
  GOSUB name=logIcon

  $r_logHtml = "$r_logHtml<p><img src='$r_iconUrl' alt=" + QUOTE("[$p_type]") + " style='border: 0px; vertical-align: middle;'/><b>"
  IF $minDate <> $maxDate
    $getMyLogRecord_result = "$r_logHtml " + dateFormat($p_date)
  ENDIF
  $r_logHtml = "$r_logHtml $d_Code - $d_Name</b> ($d_OwnerName, placed $d_PlacedDate"
  IF $d_OwnerName <> $d_PlacedBy
    $r_logHtml = "$r_logHtml by $d_PlacedBy"
  ENDIF
  $r_logHtml = "$r_logHtml)</p>$NL"
      
  $p_text = $d_lText
  GOSUB name=guessFormat
  BEGINCASE
    CASE $r_result = "HTML"
      GOSUB name=tidyHtml

    CASE $r_result = "BBCODE"
      GOSUB name=bbcodeToHtml

    CASE $r_result = "MARKDOWN"
      GOSUB name=markdownToHtml

    OTHERWISE
      GOSUB name=markdownToHtml
  ENDCASE
  $r_logHtml = "$r_logHtml<p>$r_text</p>$NL"

  # obrázky
  $p_logId = "$d_lLogId"
  GOSUB name=loadLogImages
  $p_gcCode = $d_Code
  GOSUB name=htmlLogImages
  $r_logHtml = "$r_logHtml<p>$r_logImagesHtml</p>$NL"

  $r_logHtml = "$r_logHtml</div>$NL"
ENDSUB

# @param $p_gcCode		GC kód keše
#
# @global array($IMAGES) 	Naètené obrázky
# 
# @return $rlogImagesHtml	Obrázky vyrenderované do HTML.
#
BEGINSUB name=htmlLogImages
  $r_logImagesHtml = ""

  $v_imgIndex = 1
  WHILE array($IMAGES, $v_imgIndex) <> ""
    $p_imageData = array($IMAGES, $v_imgIndex)
    GOSUB name=htmlSingleLogImage
    $r_logImagesHtml = "$r_logImagesHtml$NL$r_singleLogImageHtml"

    $v_imgIndex = $v_imgIndex + 1
  ENDWHILE

#  IF $r_logImagesHtml <> ""
##    $r_logImagesHtml = "<table>$NL$r_logImagesHtml</table>$NL"
#    $r_logImagesHtml = "<p>$NL$r_logImagesHtml</p>$NL"
#  ENDIF

  $v_x = removeVar("v_imgIndex")
ENDSUB

BEGINSUB name=htmlSingleLogImage
  $v_imgName  = EXTRACT($p_imageData, $SEP, 1)
  $v_imgDesc  = EXTRACT($p_imageData, $SEP, 2)
  $v_imgGuid  = EXTRACT($p_imageData, $SEP, 3)
  $v_imgUrl   = EXTRACT($p_imageData, $SEP, 4)
  $v_imgThumb = REPLACE("/log/", "/log/thumb/", $v_imgUrl, TRUE)
  $v_imgThumb = REGEXREPLACE("^http.*/s3\.amazonaws\.com/(.*/)?([^/]+)$", $v_imgThumb, "https://img.geocaching.com/cache/log/thumb/$2", "$")
  $v_imgThumb = REGEXREPLACE("^http.*/img\.geocaching\.com/([^/]+)$",     $v_imgThumb, "https://img.geocaching.com/cache/log/thumb/$1", "$")
  $v_imgShort = REGEXREPLACE("^$p_gcCode\s*\S*\s+", $v_imgName, "")
  $v_imgAlt   =             REGEXREPLACE("^\s*(\S.*)\s*$", $v_imgName,  " alt='$1'",   "$")
  $v_imgAlt   = $v_imgAlt + REGEXREPLACE("^\s*(\S.*)\s*$", $v_imgShort, " title='$1'", "$")
  $v_imgTitle = REGEXREPLACE("^\s*(\S.*)\s*$", $v_imgDesc, " title='$1'", "$")
  $v_imgHtml  = ""

##  $v_imgHtml = "$v_imgHtml<div>"
##  $v_imgHtml = "$v_imgHtml<div class='separator' style='float: left; clear: both;'>"
##  $v_imgHtml = "$v_imgHtml<a href='$v_imgUrl'>"
##  $v_imgHtml = "$v_imgHtml<img src='$v_imgThumb' border='0'/>"
##  $v_imgHtml = "$v_imgHtml</a>"
##  $v_imgHtml = "$v_imgHtml</div>"
##  $v_imgHtml = "$v_imgHtml<div>$v_imgName<br/><i>$v_imgDesc</i></div>"
##  $v_imgHtml = "$v_imgHtml</div>"
#
#  $v_imgHtml = "$v_imgHtml<tr>$NL"
#  $v_imgHtml = "$v_imgHtml  <td>"
#  $v_imgHtml = "$v_imgHtml    <div class='separator' style='clear: both; text-align: center;'>$NL"
  $v_imgHtml = "$v_imgHtml      <a href='$v_imgUrl'$v_imgTitle rel='prettyPhoto[logPhotos]'>$NL"
  $v_imgHtml = "$v_imgHtml        <img src='$v_imgThumb' border='0'$v_imgAlt/>$NL"
  $v_imgHtml = "$v_imgHtml      </a>$NL"
#  $v_imgHtml = "$v_imgHtml    </div>$NL"
#  $v_imgHtml = "$v_imgHtml  </td>$NL"
#  $v_imgHtml = "$v_imgHtml  <td>$v_imgName"
#  IF $v_imgDesc <> ""
#    $v_imgHtml = "$v_imgHtml$NL    <p><i>$v_imgDesc</i></p>$NL  "
#  ENDIF
#  $v_imgHtml = "$v_imgHtml  </td>$NL"
#  $v_imgHtml = "$v_imgHtml</tr>$NL"
  $r_singleLogImageHtml = $v_imgHtml

  $v_x = removeVar("v_imgName")
  $v_x = removeVar("v_imgDesc")
  $v_x = removeVar("v_imgGuid")
  $v_x = removeVar("v_imgUrl")
  $v_x = removeVar("v_imgThumb")
  $v_x = removeVar("v_imgAlt")
  $v_x = removeVar("v_imgTitle")
  $v_x = removeVar("v_imgHtml")
ENDSUB

##################################################################################################################
##                                                                                             P O M Ù C K Y    ##
##################################################################################################################

# appendSection: Doplní HTML sekci na konec promìnné $html.
#
# @param $p_sectionName		Název sekce, který bude použit k ohraníèení vkládaných dat.
# @param $p_sectionContent	Vkládaná HTML data.
#
# @global $html		Modifikovaná promìnná.
#
BEGINSUB name=appendSection
  $html = "$html$NL<!-- BEGIN: $p_sectionName -->$NL$p_sectionContent$NL<!-- END:   $p_sectionname -->"
  $v_x = removevar("p_sectionName")
  $v_x = removevar("p_sectionContent")
ENDSUB

BEGINSUB name=normalizeLogType
  BEGINCASE
  CASE $p_type = "Announcement"
       $r_type = "ANNOUNCEMENT"
  CASE $p_type = "Archive"
       $r_type = "ARCHIVED"
  CASE $p_type = "Attended"
       $r_type = "ATTENDED"
  CASE $p_type = "Didn't find it"
       $r_type = "DNF"
  CASE $p_type = "Enable Listing"
       $r_type = "ENABLED"
  CASE $p_type = "Found it"
       $r_type = "FOUND"
  CASE $p_type = "Needs Archived"
       $r_type = "ARCHIVE_REQUEST"
  CASE $p_type = "Needs Maintenance"
       $r_type = "MAINTENANCE_REQUEST"
  CASE $p_type = "Owner Maintenance"
       $r_type = "MAINTENANCED"
  CASE $p_type = "Post Reviewer Note"
       $r_type = "REVIEWER_NOTE"
  CASE $p_type = "Publish Listing"
       $r_type = "PUBLISHED"
  CASE $p_type = "Retract Listing"
       $r_type = "RETRACTED"
  CASE $p_type = "Temporarily Disable Listing"
       $r_type = "DISABLED"
  CASE $p_type = "Unarchive"
       $r_type = "UNARCHIVED"
  CASE $p_type = "Update Coordinates"
       $r_type = "UPDATED"
  CASE $p_type = "Webcam Photo Taken"
       $r_type = "PHOTO_TAKEN"
  CASE $p_type = "Will Attend"
       $r_type = "WILL_ATTEND"
  CASE $p_type = "Write note"
       $r_type = "NOTE"
  ENDCASE
ENDSUB

BEGINSUB name=logIcon
  $r_iconUrl = "http://www.xlii.cz/img/gc/icon_$p_iconType.png"
ENDSUB

BEGINSUB name=guessFormat
  $r_result = ""
  IF RegEx("<[a-zA-Z]+/?>", $p_text) OR RegEx("<[a-zA-Z]+\s>", $p_text)
    $r_result = "HTML"
  ENDIF
  IF RegEx("\[\s*[a-zA-Z*]+=?\s*\]", $p_text)
    $r_result = "BBCODE"
  ENDIF
  IF RegEx("\[[^\]]*\]\s*\(\w+://[^)]*\)", $p_text)
    $r_result = "MARKDOWN"
  ENDIF
ENDSUB

BEGINSUB name=bbcodeToHtml
  $r_text = $p_text

  #Odstranìní nechtìného textu
  #
  $r_text = RegExReplace("\n*This entry was edited by [^.]+\.", $r_text, "")
 #$r_text = RegExReplace("\s*\[size\s*=\s*1\]\s*\(dal jsem [\d.]+ \[url\s*=\s*http://www.gcvote.com\]GCVote\[/url\] body\)\[/size\]", $r_text, "")
  $r_text = RegExReplace("\s*\[size\s*=\s*1\]\s*\([^[]*\[url\s*=\s*http://www.gcvote.com\]GCVote\[/url\][^[]*\)\[/size\]", $r_text, "")
  $r_text = RegExReplace("\s*\[url\s*=\s*http://kacer-bob\..*more\.\.\..*\[/url\]", $r_text, "")

  # Smajlíky
  #
  $r_text = Replace("[:)]",  "<img src='http://www.xlii.cz/img/gc/icon_smile.gif' alt=':)' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[:D]",  "<img src='http://www.xlii.cz/img/gc/icon_smile_big.gif' alt=':D' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[:d]",  "<img src='http://www.xlii.cz/img/gc/icon_smile_big.gif' alt=':D' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[8D]",  "<img src='http://www.xlii.cz/img/gc/icon_smile_cool.gif' alt='8D' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[8d]",  "<img src='http://www.xlii.cz/img/gc/icon_smile_cool.gif' alt='8D' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[:I]",  "<img src='http://www.xlii.cz/img/gc/icon_smile_blush.gif' alt=':I' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[:i]",  "<img src='http://www.xlii.cz/img/gc/icon_smile_blush.gif' alt=':I' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[:P]",  "<img src='http://www.xlii.cz/img/gc/icon_smile_tongue.gif' alt=':P' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[:p]",  "<img src='http://www.xlii.cz/img/gc/icon_smile_tongue.gif' alt=':P' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[}:)]", "<img src='http://www.xlii.cz/img/gc/icon_smile_evil.gif' alt='}:)' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[;)]",  "<img src='http://www.xlii.cz/img/gc/icon_smile_wink.gif' alt=';)' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[:o)]", "<img src='http://www.xlii.cz/img/gc/icon_smile_clown.gif' alt=':o' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[B)]",  "<img src='http://www.xlii.cz/img/gc/icon_smile_blackeye.gif' alt='B)' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[8]",   "<img src='http://www.xlii.cz/img/gc/icon_smile_8ball.gif' alt='8' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[:(]",  "<img src='http://www.xlii.cz/img/gc/icon_smile_sad.gif' alt=':(' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[8)]",  "<img src='http://www.xlii.cz/img/gc/icon_smile_shy.gif' alt='8)' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[:O]",  "<img src='http://www.xlii.cz/img/gc/icon_smile_shock.gif' alt=':O' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[:(!]", "<img src='http://www.xlii.cz/img/gc/icon_smile_angry.gif' alt=':(!' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[xx(]", "<img src='http://www.xlii.cz/img/gc/icon_smile_dead.gif' alt='xx(' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[Xx(]", "<img src='http://www.xlii.cz/img/gc/icon_smile_dead.gif' alt='xx(' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[xX(]", "<img src='http://www.xlii.cz/img/gc/icon_smile_dead.gif' alt='xx(' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[XX(]", "<img src='http://www.xlii.cz/img/gc/icon_smile_dead.gif' alt='xx(' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[|)]",  "<img src='http://www.xlii.cz/img/gc/icon_smile_sleepy.gif' alt='|)' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[:X]",  "<img src='http://www.xlii.cz/img/gc/icon_smile_kisses.gif' alt=':X' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[:x]",  "<img src='http://www.xlii.cz/img/gc/icon_smile_kisses.gif' alt=':X' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[^]",   "<img src='http://www.xlii.cz/img/gc/icon_smile_approve.gif' alt='^' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[V]",   "<img src='http://www.xlii.cz/img/gc/icon_smile_dissapprove.gif' alt='v' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[v]",   "<img src='http://www.xlii.cz/img/gc/icon_smile_dissapprove.gif' alt='v' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[?]",   "<img src='http://www.xlii.cz/img/gc/icon_smile_question.gif' alt='?' style='border: 0px; vertical-align: middle;'/>", $r_text) 

  # HTML - styly
  $r_text = Replace("[b]",  "<b>",  $r_text)
  $r_text = Replace("[/b]", "</b>", $r_text)
  $r_text = Replace("[i]",  "<i>",  $r_text)
  $r_text = Replace("[/i]", "</i>", $r_text)
  $r_text = Replace("[u]",  "<u>",  $r_text)
  $r_text = Replace("[/u]", "</u>", $r_text)

  $r_text = Replace("[highlight]",  "<i>",  $r_text)
  $r_text = Replace("[/highlight]", "</i>", $r_text)
  $r_text = Replace("[code]",  "<pre>",  $r_text)
  $r_text = Replace("[/code]", "</pre>", $r_text)

  $r_text = Replace("[p]",  "<p>",  $r_text)
  $r_text = Replace("[/p]", "</p>", $r_text)
  $r_text = Replace("[br]",  "<br/>",  $r_text)

  # HTML - barva textu
  $r_text = RegExReplace("\[(red|green|blue|black|purple|pink|orange|yellow|white|brown|gold|maroon|navy|teal)\]",  $r_text, "<font color='$1'>",   "$")
  $r_text = RegExReplace("\[/(red|green|blue|black|purple|pink|orange|yellow|white|brown|gold|maroon|navy|teal)\]", $r_text, "<!-- /$1 --></font>", "$")

  # HTML - odrážky
  $r_text = Replace("[list]",   "<ul>",  $r_text)
  $r_text = Replace("[list=1]", "<ul style='list-style-type=decimal;'>", $r_text)
  $r_text = Replace("[*]",      "<li>",  $r_text)
  $r_text = Replace("[/list]",  "</ul>", $r_text)

  # HTML - zarovnání textu
  $r_text = Replace("[left]",  "<div align='left'>",  $r_text)
  $r_text = Replace("[/left]", "</div>", $r_text)
  $r_text = Replace("[center]",  "<div align='center'>",  $r_text)
  $r_text = Replace("[/center]", "</div>", $r_text)
  $r_text = Replace("[right]",  "<div align='right'>",  $r_text)
  $r_text = Replace("[/right]", "</div>", $r_text)

  # HTML - velikost textu
  $r_text = RegExReplace("\[size\s*=\s*", $r_text, "<font size=") 
  $r_text = Replace("[/size]", "</font>", $r_text) 

  # HTML - odkazy
  $r_text = RegExReplace("\[url\s*=\s*", $r_text, "<a href=") 
  $r_text = Replace("[/url]", "</a>", $r_text) 

  $r_text = Replace("]", ">", $r_text) 

  # Konce øádkù
  $r_text = RegExReplace("<p>(\r*\n)+", $r_text, "<p>$NL") 
  $r_text = RegExReplace("(\r*\n)+</p>", $r_text, "</p>") 
  $r_text = RegExReplace("(\r*\n)+", $r_text, "<br/>$NL") 

  # Postprocessing
  $r_text = Replace("<p></p>", "", $r_text) 
ENDSUB

BEGINSUB name=markdownToHtml
  $r_text = $p_text

  #Odstranìní nechtìného textu
  #
  $r_text = RegExReplace("\n*This entry was edited by [^.]+\.",                                 $r_text, "")
  $r_text = RegExReplace("\s*\([^(]*\[\s*GCVote\s*\]\s*\(\s*http://www.gcvote.com\s*\)[^)]*\)", $r_text, "")
  $r_text = RegExReplace("\s*\**\[[^\]]*\]\s*\**\s*\(\s*http://kacer-bob\.[^)]+\)\s*\**",       $r_text, "")
  $r_text = RegExReplace("\[\s*\**[^\]]*\**\s*\]\s*\(\s*http://kacer-bob\.[^)]+\)\s*\**",       $r_text, "")

  # HTML - odrážky
  $r_text = RegExReplace("(\r*\n)(\*\s\s*.*)(\r*\n)([^*])",      $r_text,  "<ul>\2</ul>\3\4",             "\")
  $r_text = RegExReplace("^\*\s*(.*)$",                          $r_text,  "<li>\1</li>",                 "\")
  #
  $r_text = RegExReplace("(\r*\n)(\d+\s*\.\s+.*)(\r*\n)([^\d])", $r_text,  "<ol>\2</ol>\3\4",             "\")
  $r_text = RegExReplace("^(\d+)\.\s*(.*)$",                     $r_text,  "<li value='\1'>\2</li>",      "\")
  #
  $r_text = RegExReplace("(\r*\n)>(.*)(\r*\n)([^>])",            $r_text,  "<blockquote style='border-left: 2px solid darkgreen; font-size: 14pt; padding-left: 10pt;'>\2</blockquote>", "\")
  $r_text = RegExReplace("(\r*\n)>\s*(.*)$",                     $r_text,  "<div>\1</div>",               "\")

  # HTML - styly
  $r_text = RegExReplace("(^|\W)\*\*([^*]*)\*\*($|\W)", $r_text, "\1<b>\2</b>\3", "\")
  $r_text = RegExReplace("(^|\W)\*([^*]*)\*($|\W)",  $r_text, "\1<i>\2</i>\3", "\")
  $r_text = RegExReplace("(^|\W)__([^_]*)__($|\W)", $r_text, "\1<b>\2</b>\3", "\")
  $r_text = RegExReplace("(^|\W)_([^_]*)_($|\W)",  $r_text, "\1<i>\2</i>\3", "\")

  # HTML - odkazy
  $r_text = RegExReplace("\[([^\]]*)\]\s*\(\s*([^)]*)\s*\)", $r_text, "<a href='\2'>\1</a>", "\") 

  # Konce øádkù
  $r_text = RegExReplace("<p>(\r*\n)+",  $r_text, "<p>$NL") 
  $r_text = RegExReplace("(\r*\n)+</p>", $r_text, "</p>") 
  $r_text = RegExReplace("(\r*\n)+",     $r_text, "<br/>$NL") 

  # Jiné
  $r_text = RegExReplace("^---+", $r_text, "<hr/>") 

  # Smajlíky
  #
  $r_text = Replace("[:)]",  "<img src='http://www.xlii.cz/img/gc/icon_smile.gif' alt=':)' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[:D]",  "<img src='http://www.xlii.cz/img/gc/icon_smile_big.gif' alt=':D' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[:d]",  "<img src='http://www.xlii.cz/img/gc/icon_smile_big.gif' alt=':D' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[8D]",  "<img src='http://www.xlii.cz/img/gc/icon_smile_cool.gif' alt='8D' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[8d]",  "<img src='http://www.xlii.cz/img/gc/icon_smile_cool.gif' alt='8D' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[:I]",  "<img src='http://www.xlii.cz/img/gc/icon_smile_blush.gif' alt=':I' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[:i]",  "<img src='http://www.xlii.cz/img/gc/icon_smile_blush.gif' alt=':I' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[:P]",  "<img src='http://www.xlii.cz/img/gc/icon_smile_tongue.gif' alt=':P' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[:p]",  "<img src='http://www.xlii.cz/img/gc/icon_smile_tongue.gif' alt=':P' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[}:)]", "<img src='http://www.xlii.cz/img/gc/icon_smile_evil.gif' alt='}:)' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[;)]",  "<img src='http://www.xlii.cz/img/gc/icon_smile_wink.gif' alt=';)' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[:o)]", "<img src='http://www.xlii.cz/img/gc/icon_smile_clown.gif' alt=':o' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[B)]",  "<img src='http://www.xlii.cz/img/gc/icon_smile_blackeye.gif' alt='B)' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[8]",   "<img src='http://www.xlii.cz/img/gc/icon_smile_8ball.gif' alt='8' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[:(]",  "<img src='http://www.xlii.cz/img/gc/icon_smile_sad.gif' alt=':(' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[8)]",  "<img src='http://www.xlii.cz/img/gc/icon_smile_shy.gif' alt='8)' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[:O]",  "<img src='http://www.xlii.cz/img/gc/icon_smile_shock.gif' alt=':O' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[:(!]", "<img src='http://www.xlii.cz/img/gc/icon_smile_angry.gif' alt=':(!' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[xx(]", "<img src='http://www.xlii.cz/img/gc/icon_smile_dead.gif' alt='xx(' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[Xx(]", "<img src='http://www.xlii.cz/img/gc/icon_smile_dead.gif' alt='xx(' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[xX(]", "<img src='http://www.xlii.cz/img/gc/icon_smile_dead.gif' alt='xx(' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[XX(]", "<img src='http://www.xlii.cz/img/gc/icon_smile_dead.gif' alt='xx(' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[|)]",  "<img src='http://www.xlii.cz/img/gc/icon_smile_sleepy.gif' alt='|)' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[:X]",  "<img src='http://www.xlii.cz/img/gc/icon_smile_kisses.gif' alt=':X' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[:x]",  "<img src='http://www.xlii.cz/img/gc/icon_smile_kisses.gif' alt=':X' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[^]",   "<img src='http://www.xlii.cz/img/gc/icon_smile_approve.gif' alt='^' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[V]",   "<img src='http://www.xlii.cz/img/gc/icon_smile_dissapprove.gif' alt='v' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[v]",   "<img src='http://www.xlii.cz/img/gc/icon_smile_dissapprove.gif' alt='v' style='border: 0px; vertical-align: middle;'/>", $r_text) 
  $r_text = Replace("[?]",   "<img src='http://www.xlii.cz/img/gc/icon_smile_question.gif' alt='?' style='border: 0px; vertical-align: middle;'/>", $r_text) 

  # Postprocessing
  $r_text = Replace("<p>\s*</p>", "", $r_text) 
ENDSUB

BEGINSUB name=tidyHtml
  $r_text = $p_text

  #Odstranìní nechtìného textu
  #
  $r_text = RegExReplace("This entry was edited by [^.]+\.", $r_text, "")
  $r_text = RegExReplace("\([^)]*<a\s+href\s*=\s*'http://www.gcvote.com\.*</a>[^)]*\)", $r_text, "")
  $r_text = RegExReplace("<a\s+href\s*=\s*'http://kacer-bob\..*</a>", $r_text, "")
ENDSUB

# Zmìní velikost pole na zadaný poèet prvkù.
# Nutné napø. tehdy, když potøebujete setøídit neúplnì zaplnìné pole.
#
# @param $p_array  Index pole, které má být zkráceno.
# @param $p_count  Poèet prvkù zkracovaného pole.
BEGINSUB name=shrinkArray
  array($WORK, 0) = "$p_count"
  $i = 1
  WHILE $i <= $p_count
    array($WORK, $i) = array($p_array, $i)
    $i = $i + 1
  ENDWHILE

  array($p_array, 0) = "$p_count"
  $i = 1
  WHILE $i <= $p_count
    array($p_array, $i) = array($WORK, $i)
    $i = $i + 1
  ENDWHILE

  array($WORK, 0) = "0"
ENDSUB
##################################################################################################################

BEGINSUB name=getLimittingDates
   $minDate = [99991231]
   $maxDate = [00000000]

   GOTO position=TOP
   WHILE NOT($_EOL)
      IF ($_FilterActive OR $d_UserFlag) AND $d_FoundByMeDate <> [00000000]
         IF $d_FoundByMeDate < $minDate
            $minDate = $d_FoundByMeDate
         ENDIF
         IF $d_FoundByMeDate > $maxDate
            $maxDate = $d_FoundByMeDate
         ENDIF
      ENDIF
      GOTO position=NEXT
   ENDWHILE
ENDSUB

BEGINSUB name=initSearchByUrl
  $v_blogs = list($BLOGS, "create", ";")
  $g_blogsLoaded = false
  IF NOT($chbReloadBlogs)
    MacSettings type=R vars=v_blogs,g_blogsLoaded fileCheck=N
  ENDIF

  IF NOT($g_blogsLoaded)
    IF $VERBOSE
      PAUSE msg="GOSUB name=loadBlogData"
    ENDIF
    GOSUB name=loadBlogData
    $v_blogs = list($BLOGS, "sort", "")
    MacSettings type=S vars=v_blogs,g_blogsLoaded
  ENDIF
  $v_blogs = list($BLOGS, "replace", $v_blogs)

  $v_url = regexReplace("^(\d+)/(\d+)/(.*)$", $p_url, "$3,$1,$2", "$")
  $v_index = list($BLOGS, "regex", "^.*,$v_url")
  IF $v_index = "0" OR $v_index = ""
    RETURN msg="URL '$p_url' neodpovídají žádné keše."
  ENDIF

  $v_item  = list($BLOGS, "item", $v_index)
  $v_from  = regexsub($BLOGITEM_RE, $v_item, 1, $BLOGITEM_DATE1_GRP)
  $v_to    = regexsub($BLOGITEM_RE, $v_item, 1, $BLOGITEM_DATE2_GRP)
  $v_year  = regexsub($BLOGITEM_RE, $v_item, 1, $BLOGITEM_YEAR_GRP)
  $v_month = regexsub($BLOGITEM_RE, $v_item, 1, $BLOGITEM_MONTH_GRP)

  IF $v_url = $p_url
    $blogName  = $p_url
    $blogYear  = ""
    $blogMonth = ""
  ELSE
    $blogName  = regexSub("^(\d+)/(\d+)/(.*)$", $p_url, 1, 3)
    $blogYear  = regexSub("^(\d+)/(\d+)/(.*)$", $p_url, 1, 1)
    $blogMonth = regexSub("^(\d+)/(\d+)/(.*)$", $p_url, 1, 2)
  ENDIF
  $minDate = stringToDate($v_from)
  $maxDate = stringToDate($v_to)

  # úklid
  $v_blogs = list($BLOGS, "destroy", "")
  $v_x = removeVar("v_blogs")
  $v_x = removeVar("v_url")
  $v_x = removeVar("v_index")
  $v_x = removeVar("v_item")
  $v_x = removeVar("v_from")
  $v_x = removeVar("v_to")
ENDSUB

BEGINSUB name=loadBlogData
  $v_blogCount0 = VAL(LIST($BLOGS, "count", ""))
  $v_count = 0

  $r = SQLITE("sql", "SELECT c.code, c.name, m.lText, l.lDate FROM caches c JOIN logs l ON c.code = l.lParent JOIN logMemo m ON l.lParent = m.lParent AND l.lLogId = m.lLogId WHERE (c.found <> 0 OR c.dnf <> 0) AND l.lIsOwner ORDER BY c.code ASC, l.lLogId ASC", "sqlget=0")
  WHILE NOT($_SQLEOL)
      $p_text = SQLGET("lText")
      GOSUB name=getBlogUrl
      $v_url   = $r_url
      $v_year  = $r_year
      $v_month = $r_month

      IF $v_url <> ""
        $v_count = $v_count + 1
        $v_cacheCode = SQLGET("code")
        $v_cacheName = SQLGET("name")
        $v_logDate = SQLTODATE(SQLGET("lDate"))

        SHOWSTATUS title="Vyhledání názvù blogù" msg="#$v_count $v_cacheCode: $v_url ($v_logDate)" width=500 height=500 mtyp=MULTI
        GOSUB name=showStatus

        $v_index = list($BLOGS, "regex", ".*,$v_url")
        IF $v_index = "0" OR $v_index = ""
          $v_from  = dateToString($v_logDate)
          $v_to    = dateToString($v_logDate)
          $v_item  = "$v_from,$v_to,$v_url"
          $v_blogs = list($BLOGS, "add", $v_item)
        ELSE
          $v_item = list($BLOGS, "item", $v_index)
          $v_from = regexsub($BLOGITEM_RE, $v_item, 1, $BLOGITEM_DATE1_GRP)
          $v_to   = regexsub($BLOGITEM_RE, $v_item, 1, $BLOGITEM_DATE2_GRP)

          IF stringToDate($v_from) > $v_logDate
            $v_from = dateToString($v_logDate)
          ENDIF
          IF stringToDate($v_to) < $v_logDate
            $v_to = dateToString($v_logDate)
          ENDIF
          $v_item = "$v_from,$v_to,$v_url,$v_year,$v_month"
          $v_blogs = list($BLOGS, "edit:$v_index", $v_item)
        ENDIF
      ENDIF
    SQLNEXT
  ENDWHILE

  SQLGETCLOSE
  SHOWSTATUS display=OFF

  $g_blogsLoaded = true
  $v_blogCount9 = VAL(LIST($BLOGS, "count", ""))
  $v_newBlogCount = $v_blogCount9 - $v_blogCount0
#  MSGOK title="Vyhledání názvù blogù" msg="Nalezeno $v_newBlogCount nových blogù (døíve $v_blogCount0, nyní $v_blogCount9)."

  # úklid
  $v_x = removeVar("v_cacheName")
  $v_x = removeVar("v_cacheCode")
  $v_x = removeVar("v_url")
  $v_x = removeVar("v_year")
  $v_x = removeVar("v_month")
  $v_x = removeVar("v_logDate")
  $v_x = removeVar("v_index")
  $v_x = removeVar("v_item")
  $v_x = removeVar("v_from")
  $v_x = removeVar("v_to")
  $v_x = removeVar("v_blogCount0")
  $v_x = removeVar("v_blogCount9")
  $v_x = removeVar("v_newBlogCount")
  $v_x = removeVar("v_count")
ENDSUB

BEGINSUB name=getBlogUrl
#MSGOK msg=" p_text: $p_text$NL URL_RE: $URL_RE$NL URL_NAME_GRP: $URL_NAME_GRP$NL  URL_YEAR_GRP: $URL_YEAR_GRP$NL  URL_MONTH_GRP: $URL_MONTH_GRP$NL"
  $r_url   = regexsub($URL_RE, $p_text, 1, $URL_NAME_GRP)
  $r_year  = regexsub($URL_RE, $p_text, 1, $URL_YEAR_GRP)
  $r_month = regexsub($URL_RE, $p_text, 1, $URL_MONTH_GRP)
ENDSUB

BEGINSUB name=loadLogImages
  array($IMAGES, 0) = "100"
  $v_imgIndex = 0

  $r = SQLITE("sql", "SELECT iName, iDescription, iGuid, iImage FROM LogImages WHERE iLogId = $p_logId", "sqlget=1")
  WHILE NOT($_SQLEOL1)
    $v_imgIndex = $v_imgIndex + 1
    $v_imgName  = SQLGET("iName", 1)
    $v_imgDesc  = SQLGET("iDescription", 1)
    $v_imgGuid  = SQLGET("iGuid", 1)
    $v_imgUrl   = SQLGET("iImage", 1)
    array($IMAGES, $v_imgIndex) = "$v_imgName$SEP$v_imgDesc$SEP$v_imgGuid$SEP$v_imgUrl"
    SQLNEXT handle=1
  ENDWHILE
  SQLGETCLOSE handle=1

  $v_x = removeVar("v_imgIndex")
  $v_x = removeVar("v_imgName")
  $v_x = removeVar("v_imgDesc")
  $v_x = removeVar("v_imgGuid")
  $v_x = removeVar("v_imgUrl")
ENDSUB

BEGINSUB name=VstupniFormular
  $rbtAll = true
  $rbtLogDate = false
  $rbtLogUrl = false
  $chbVerbose = $VERBOSE
  $chbHeading = true
  $chbTable = true
  $chbLogs = true
  MacSettings type=R vars=rbtAll,rbtLogDate,rbtLogUrl,chbHeading,chbTable,chbLogs,datFrom,datUpto,txtLogUrl file=BR_HtmlLogBase2_form.xml fileCheck=N

  $VstupniFormular = editForm($VstupniFormular, "frmVstupniFormular", "delay", "7")
  WHILE true
    $exit = form($VstupniFormular, "")
    $VstupniFormular = editForm($VstupniFormular, "frmVstupniFormular", "delay", "")
    $VERBOSE = $chbVerbose
    IF $VERBOSE
      PAUSE msg=" rbtAll: >$rbtAll<$NL rbtLogDate: >$rbtLogDate<$NL datFrom: >$datFrom<$NL datUpto: >$datUpto<$NL rbtLogUrl: >$rbtLogUrl<$NL txtLogUrl: >$txtLogUrl<$NL btnPickBlog: >$btnPickBlog<$NL chbReloadBlogs: >$chbReloadBlogs<$NL chbVerbose: >$chbVerbose<$NL chbHeading: >$chbHeading<$NL chbTable: >$chbTable<$NL chbLogs: >$chbLogs<$NL btnOk: >$btnOk<$NL btnCancel: >$btnCancel<$NL"
    ENDIF

    BEGINCASE
    CASE $exit = "btnPickBlog"
      GOSUB name=pickBlogName
      $txtLogUrl = $result
    CASE $exit = "btnOk" OR $exit = "DelayExit"
      BREAK
    CASE $exit = "btnCancel" OR $exit = "SystemExit"
      RETURN

    CASE $exit = "datFrom"
      $datUpto = $datFrom
    ENDCASE
  ENDWHILE

  MacSettings type=S vars=rbtAll,rbtLogDate,rbtLogUrl,chbHeading,chbTable,chbLogs,datFrom,datUpto,txtLogUrl file=BR_HtmlLogBase2_form.xml 
ENDSUB

<Data> VarName=$VstupniFormular
#********************************************************************
# Form generated by GSAK form designer on so 19-III-2011 10:14:38
#********************************************************************

Name = frmVstupniFormular
  Type = Form
  Caption = Tabulky do blogu
  Height = 215
  Width = 350
  Delay = 7

Name = Groupbox1
  Type = Groupbox
  Caption = Co všechno generovat
  Height = 97
  Left = 192
  Top = 8
  Width = 137
  Taborder = 12

Name = rbtAll
  Type = Radiobutton
  Height = 17
  Left = 8
  Top = 8
  Width = 113
  Taborder = 8
  ExitOnChange = Yes
  Caption = Všechny ve výbìru

Name = rbtLogDate
  Type = Radiobutton
  Height = 17
  Left = 8
  Top = 32
  Width = 113
  Taborder = 9
  ExitOnChange = Yes
  Caption = Podle data logu

Name = Label1
  Type = Label
  Height = 13
  Left = 24
  Top = 56
  Width = 12
  Caption = od

Name = datFrom
  Type = Date
  Height = 21
  Left = 48
  Top = 51
  Width = 121
  Taborder = 10
#  ExitOnChange = Yes

Name = Label2
  Type = Label
  Height = 13
  Left = 24
  Top = 80
  Width = 12
  Caption = do

Name = datUpto
  Type = Date
  Height = 21
  Left = 48
  Top = 75
  Width = 121
  Taborder = 11

Name = rbtLogUrl
  Type = Radiobutton
  Height = 17
  Left = 8
  Top = 104
  Width = 113
  Taborder = 12
  ExitOnChange = Yes
  Caption = Podle URL v logu

Name = LabelLogUrl
  Type = Label
  Height = 13
  Left = 24
  Top = 128
  Width = 12
  Caption = URL

Name = txtLogUrl
  Type = Edit
  Height = 17
  Left = 48
  Top = 123
  Width = 265
  Taborder = 13

Name = btnPickBlog
  Type = Button
  Escape = Yes
  Height = 25
  Left = 317
  Top = 121
  Width = 16
  Taborder = 14
  Caption = ...

Name = chbReloadBlogs
  Type = Checkbox
  Height = 17
  Left = 24
  Top = 147
  Width = 150
  Taborder = 15
  Caption = obnovit data blogù

Name = chbVerbose
  Type = Checkbox
  Height = 17
  Left = 8
  Top = 164
  Width = 150
  Taborder = 15
  Caption = prùbìžná hlášení (verbose)

Name = chbHeading
  Type = Checkbox
  Height = 17
  Left = 200
  Top = 32
  Width = 97
  Taborder = 16
  Caption = záhlaví (souhrn)

Name = chbTable
  Type = Checkbox
  Height = 17
  Left = 200
  Top = 56
  Width = 110
  Taborder = 17
  Caption = podrobnou tabulku

Name = chbLogs
  Type = Checkbox
  Height = 17
  Left = 200
  Top = 80
  Width = 97
  Taborder = 18
  Caption = logy

Name = btnOk
  Type = Button
  Enter = Yes
  Height = 25
  Left = 168
  Top = 157
  Width = 75
  Taborder = 19
  Caption = Ok

Name = btnCancel
  Type = Button
  Escape = Yes
  Height = 25
  Left = 256
  Top = 157
  Width = 75
  Taborder = 20
  Caption = Storno

<enddata>

BEGINSUB name=pickBlogName
  GOSUB name=fillList
  WHILE true
    $exit = form($SeznamBlogu, "")
    BEGINCASE
    CASE $exit = "btnOk"
      $p_item = $lstBlogs
      GOSUB name=parseSelectedItem

      IF $r_name <> ""
        $p_name = $r_name
        $p_from = $r_from
        $p_to   = $r_to
        GOSUB name=lookupBlogItem

        $result = $r_name
        IF $r_year <> "" AND $r_month <> ""
          $result = "$r_year/$r_month/$result"
        ENDIF

        BREAK
      ENDIF

    CASE $exit = "btnCancel"
      EXITSUB

    CASE $exit = "btnReload"
      $g_blogsLoaded = false
      $p_url = ".*"
      GOSUB name=initSearchByUrl
      GOSUB name=fillList
    ENDCASE
  ENDWHILE
ENDSUB

BEGINSUB name=parseSelectedItem
  IF AT(";", $p_item) > 0
    MSGOK msg="Vyberte, prosím, jen jednu položku."
    $r_name = ""
    EXITSUB
  ENDIF

  $v_name  = regexsub("^([^ ]+) +\((\d+)\.(\d+)\.(\d+)( *- *(\d+)\.(\d+)\.(\d+))?", $p_item, 1, 1)
  $v_from1 = regexsub("^([^ ]+) +\((\d+)\.(\d+)\.(\d+)( *- *(\d+)\.(\d+)\.(\d+))?", $p_item, 1, 2)
  $v_from2 = regexsub("^([^ ]+) +\((\d+)\.(\d+)\.(\d+)( *- *(\d+)\.(\d+)\.(\d+))?", $p_item, 1, 3)
  $v_from3 = regexsub("^([^ ]+) +\((\d+)\.(\d+)\.(\d+)( *- *(\d+)\.(\d+)\.(\d+))?", $p_item, 1, 4)
  $v_to1   = regexsub("^([^ ]+) +\((\d+)\.(\d+)\.(\d+)( *- *(\d+)\.(\d+)\.(\d+))?", $p_item, 1, 6)
  $v_to2   = regexsub("^([^ ]+) +\((\d+)\.(\d+)\.(\d+)( *- *(\d+)\.(\d+)\.(\d+))?", $p_item, 1, 7)
  $v_to3   = regexsub("^([^ ]+) +\((\d+)\.(\d+)\.(\d+)( *- *(\d+)\.(\d+)\.(\d+))?", $p_item, 1, 8)

  $r_name = $v_name
  $r_from = stringToDate($v_from3+REPLACE(" ", "0", STR(VAL($v_from2), 2, 0))+REPLACE(" ", "0", STR(VAL($v_from1), 2, 0)))
  IF $v_to1 = ""
    $r_to = $r_from
  ELSE
    $r_to = stringToDate($v_to3+REPLACE(" ", "0", STR(VAL($v_to2), 2, 0))+REPLACE(" ", "0", STR(VAL($v_to1), 2, 0)))
  ENDIF

  $v_x = removeVar("v_name")
  $v_x = removeVar("v_from1")
  $v_x = removeVar("v_from2")
  $v_x = removeVar("v_from3")
  $v_x = removeVar("v_to1")
  $v_x = removeVar("v_to2")
  $v_x = removeVar("v_to3")
ENDSUB

BEGINSUB name=lookupBlogItem
  $v_vzor = dateToString($p_from)+","+dateToString($p_to)+","+$p_name

  $v_blogs = list($BLOGS, "create", ";")
  MacSettings type=R vars=v_blogs,g_blogsLoaded fileCheck=N
  $v_blogs = list($BLOGS, "replace", $v_blogs)

  $v_index = list($BLOGS, "regex", "^$v_vzor(,\d+,\d+)?$")
  IF $v_index = "0"
    $r_name  = ""
    $r_year  = ""
    $r_month = ""
  ELSE
    $v_item  = list($BLOGS, "item", $v_index)
    $r_name  = regexsub($BLOGITEM_RE, $v_item, 1, $BLOGITEM_NAME_GRP)
    $v_year  = regexsub($BLOGITEM_RE, $v_item, 1, $BLOGITEM_YEAR_GRP)
    $v_month = regexsub($BLOGITEM_RE, $v_item, 1, $BLOGITEM_MONTH_GRP)
    IF $v_year = ""
      $r_year  = ""
      $r_month = ""
    ELSE
      $r_year  = $v_year
      $r_month = $v_month
    ENDIF
  ENDIF

  # úklid
  $v_blogs = list($BLOGS, "destroy", "")
  $v_x = removeVar("v_blogs")
  $v_x = removeVar("v_vzor")
  $v_x = removeVar("v_index")
  $v_x = removeVar("v_item")
  $v_x = removeVar("v_year")
  $v_x = removeVar("v_month")
ENDSUB

BEGINSUB name=fillList
  $v_blogs = list($BLOGS, "create", ";")
  MacSettings type=R vars=v_blogs,g_blogsLoaded fileCheck=N
  $v_blogs = list($BLOGS, "replace", $v_blogs)
  $v_blogs = list($BLOGS, "sort", "")
  $v_blogs = list($BLOGS, "replace", $v_blogs)

  $v_pocet = val(list($BLOGS, "count", ""))
  $v_pozice = $v_pocet
  $v_values = ""
  $v_items = list("V_ITEMS", "create", ",")
  WHILE $v_pozice > 0
    $v_item = list($BLOGS, "item", numToStr($v_pozice))
    $v_items = list("V_ITEMS", "replace", $v_item)
    $v_blog = list("V_ITEMS", "item", "3")
    $v_from = stringToDate(list("V_ITEMS", "item", "1"))
    $v_to = stringToDate(list("V_ITEMS", "item", "2"))

    IF $v_from = $v_to
      $v_values = "$v_values;$v_blog ($v_from)"
    ELSE
      $v_values = "$v_values;$v_blog ($v_from - $v_to)"
    ENDIF

    $v_pozice = $v_pozice - 1
  ENDWHILE
  $v_values = substr($v_values, 2, 0)
  
  $SeznamBlogu = editForm($SeznamBlogu, "lstBlogs", "values", $v_values)

  $v_blogs = list($BLOGS, "destroy", "")
  $v_items = list("V_ITEMS", "destroy", "")
  $v_x = removeVar("v_blogs")
  $v_x = removeVar("v_pozice")
  $v_x = removeVar("v_pocet")
  $v_x = removeVar("v_values")
  $v_x = removeVar("v_item")
  $v_x = removeVar("v_items")
  $v_x = removeVar("v_blog")
  $v_x = removeVar("v_from")
  $v_x = removeVar("v_to")
ENDSUB

BEGINSUB name=showStatus
  SHOWSTATUS msg=$p_msg width=500 height=500 mtype=MULTI
ENDSUB

BEGINSUB name=showStatus_emptyLine
  $p_msg = ""
  GOSUB name=showStatus
ENDSUB

<Data> VarName=$SeznamBlogu
#********************************************************************
# Form generated by GSAK form designer on ne 27-XI-2011 22:33:31
#********************************************************************

Name = frmSeznamBlogu
  Type = Form
  Height = 715
  Top = 224
  Width = 474

Name = lstBlogs
  Type = Checklistbox
  Height = 665
  Left = 8
  Top = 8
  Width = 361
  Taborder = 10

Name = btnOk
  Type = Button
  Height = 25
  Left = 376
  Top = 8
  Width = 75
  Taborder = 11
  Caption = Ok

Name = btnCancel
  Type = Button
  Escape = Yes
  Height = 25
  Left = 376
  Top = 40
  Width = 75
  Taborder = 12
  Caption = Storno

Name = btnReload
  Type = Button
  Height = 25
  Left = 376
  Top = 96
  Width = 75
  Taborder = 13
  Caption = Reload

<enddata>
#
